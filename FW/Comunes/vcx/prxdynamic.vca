SCCTEXT Version 4.0.0.2
PLATFORM C(8,0),UNIQUEID C(10,0),TIMESTAMP N(10,0),CLASS M(4,0),CLASSLOC M(4,0),BASECLASS M(4,0),OBJNAME M(4,0),PARENT M(4,0),PROPERTIES M(4,0),PROTECTED M(4,0),METHODS M(4,0),OBJCODE M(4,0),OLE M(4,0),OLE2 M(4,0),RESERVED1 M(4,0),RESERVED2 M(4,0),RESERVED3 M(4,0),RESERVED4 M(4,0),RESERVED5 M(4,0),RESERVED6 M(4,0),RESERVED7 M(4,0),RESERVED8 M(4,0),USER M(4,0)
1252

[ RECORD]
[PLATFORM] COMMENT 
[UNIQUEID] Class     
[START RESERVED1]
VERSION =   3.00[END RESERVED1]

[ RECORD]
[PLATFORM] WINDOWS 
[UNIQUEID] _2Q417OGA6
[CLASS] abmgenericform
[CLASSLOC] prxmainform.vcx
[BASECLASS] form
[OBJNAME] abmdynamicform
[START PROPERTIES]
Height = 290
Width = 602
DoCreate = .T.
Caption = "ABM Dynamic Form"
ocolevents = .NULL.
ocoldefaultparameters = .NULL.
_memberdata =     2578<VFPData>
	<memberdata name="npaddingtop" type="property" display="nPaddingTop"/>
	<memberdata name="npaddingbottom" type="property" display="nPaddingBottom"/>
	<memberdata name="npaddingleft" type="property" display="nPaddingLeft"/>
	<memberdata name="npaddingright" type="property" display="nPaddingRight"/>
	<memberdata name="creportall" type="property" display="cReportAll"/>
	<memberdata name="creportone" type="property" display="cReportOne"/>
	<memberdata name="cDataConfigurationKey" type="property" display="cDataConfigurationKey"/><memberdata name="oentity_access" type="method" display="oEntity_Access"/><memberdata name="populateproperties" type="method" display="PopulateProperties"/><memberdata name="lfirsttime" type="property" display="lFirstTime"/><memberdata name="ocolevents" type="property" display="oColEvents"/><memberdata name="ocolevents_access" type="method" display="oColEvents_Access"/><memberdata name="ocoldefaultparameters" type="property" display="oColDefaultParameters"/><memberdata name="ocoldefaultparameters_access" type="method" display="oColDefaultParameters_Access"/><memberdata name="addcode" type="method" display="AddCode"/><memberdata name="addeventcode" type="method" display="AddEventCode"/><memberdata name="addorgetcontrol" type="method" display="AddOrGetControl"/><memberdata name="addorgetevent" type="method" display="AddOrGetEvent"/><memberdata name="clearbindevents" type="method" display="ClearBindEvents"/><memberdata name="filldefaultparameters" type="method" display="FillDefaultParameters"/><memberdata name="getpath" type="method" display="GetPath"/><memberdata name="getref" type="method" display="GetRef"/><memberdata name="getrefr" type="method" display="GetRefR"/><memberdata name="handlerevent" type="method" display="HandlerEvent"/><memberdata name="preparecode" type="method" display="PrepareCode"/><memberdata name="addobjectto" type="method" display="AddObjectTo"/><memberdata name="applyproperty" type="method" display="ApplyProperty"/><memberdata name="applypropertiesto" type="method" display="ApplyPropertiesTo"/><memberdata name="loadconfig" type="method" display="LoadConfig"/><memberdata name="loadcontrols" type="method" display="LoadControls"/><memberdata name="loadmethods" type="method" display="LoadMethods"/><memberdata name="loadproperties" type="method" display="LoadProperties"/><memberdata name="cloneobject" type="method" display="CloneObject"/><memberdata name="cloneobj" type="method" display="CloneObj"/><memberdata name="setclasslib" type="method" display="SetClassLib"/></VFPData>

Name = "abmdynamicform"
cmdDebugInfo.Name = "cmdDebugInfo"
optionpanel.Shape.Name = "Shape"
optionpanel.shpLabel.Name = "shpLabel"
optionpanel.lblTitulo.Name = "lblTitulo"
optionpanel.cmdClose.Name = "cmdClose"
optionpanel.cmdDelete.Name = "cmdDelete"
optionpanel.cmdNew.Name = "cmdNew"
optionpanel.cmdSave.Name = "cmdSave"
optionpanel.cmdOpen.Name = "cmdOpen"
optionpanel.cmdReport.Name = "cmdReport"
optionpanel.cmdEdit.Name = "cmdEdit"
optionpanel.Top = 228
optionpanel.Left = 12
optionpanel.Name = "optionpanel"
Navigator.Shape.Name = "Shape"
Navigator.shpLabel.Name = "shpLabel"
Navigator.lblTitulo.Name = "lblTitulo"
Navigator.cmdFirst.Name = "cmdFirst"
Navigator.cmdPrior.Name = "cmdPrior"
Navigator.cmdNext.Name = "cmdNext"
Navigator.cmdLast.Name = "cmdLast"
Navigator.ORDERBY.Name = "ORDERBY"
Navigator.picExpand.Height = 32
Navigator.picExpand.Width = 16
Navigator.picExpand.Name = "picExpand"
Navigator.picContract.Height = 32
Navigator.picContract.Width = 16
Navigator.picContract.Name = "picContract"
Navigator.Top = 180
Navigator.Left = 12
Navigator.Name = "Navigator"
[END PROPERTIES]
[START PROTECTED]
ocolevents
ocoldefaultparameters
addorgetcontrol
addorgetevent
clearbindevents
filldefaultparameters
getpath
getref
getrefr
preparecode
applyproperty
applypropertiesto
loadcontrols
loadmethods
loadproperties
cloneobj
CloneObject
[END PROTECTED]
[START METHODS]
PROCEDURE Destroy

This.lOnExit = .T.
This.ClearBindEvents()

DoDefault()

ENDPROC
PROCEDURE QueryUnload

#INCLUDE "FW\TierAdapter\Include\TA.h"

DoDefault()

With This As mainform Of FW\Comunes\vcx\prxmainform.vcx
	If .nStatus = IS_IDLE Or .oGlobalSettings.oApp.lForcedExit
		* Everything ok
		This.lOnExit = .T.
		This.ClearBindEvents()
	Else
		* Aborted
		NoDefault
		
	EndIf && .nStatus = IS_IDLE Or .oGlobalSettings.oApp.lForcedExit
	
Endwith

ENDPROC
PROCEDURE addcode
Lparameters toControl As Collection, tcCode As String, tnPrioridad As Number

Local i As Integer
Local lcKey As String
Local oErr As Exception
Local loError As ErrorHandler Of "Tools\ErrorHandler\Prg\ErrorHandler.prg"

Try

	If Vartype( tnPrioridad ) # 'N' Or Empty( tnPrioridad )
		tnPrioridad = 99

	Endif

	tnPrioridad = Max( Min( tnPrioridad, 99 ), 1 )
	lcKey = Transform( tnPrioridad )
	i = toControl.GetKey( lcKey )
	If Empty( i )
		If toControl.Count > 0
			toControl.Add( tcCode, lcKey, lcKey )

		Else
			toControl.Add( tcCode, lcKey )

		Endif
	Else
		j = 0
		lcKeyNew = lcKey + Chr( 64 + j )
		i = toControl.GetKey( lcKeyNew )
		Do While ! Empty( i )
			lcKeyNew = lcKey + Chr( 64 + j )
			i = toControl.GetKey( lcKeyNew )
			j = j + 1

		Enddo
		toControl.Add( tcCode, lcKeyNew )

	Endif && Empty( i )

Catch To oErr
	loError = This.oError
	This.cXMLoError = loError.Process( oErr )
	Throw loError

Finally
	loError = This.oError
	loError.Cremark = ''
	loError.Ctracelogin = ''
	loError = Null

Endtry

ENDPROC
PROCEDURE addeventcode
Lparameters tcObject As String, ;
	tcEvent As String, ;
	tcCode As String, ;
	tnPrioridad As Number

#INCLUDE "FW\Comunes\Include\Praxis.h"

Local loObject As Object
Local oErr As Exception
Local lcObjPath As String
Local loEvent As Collection
Local loControl As Collection
Local lcCode As String
Local loErr As Exception
Local llOk As Boolean
Local lcMessage As String
Local loError As ErrorHandler Of "Tools\ErrorHandler\Prg\ErrorHandler.prg"

Try
	loObject = This.GetRef( tcObject )

	tcEvent = Alltrim( tcEvent )

	If Pemstatus( loObject, tcEvent, 5 )
		llOk = .T.

		lcObjPath = This.GetPath( loObject )

		* Preparo el Codigo
		lcCode = This.PrepareCode( tcCode )

		* Obtengo el Evento
		loEvent = This.AddOrGetEvent( tcEvent )
		loControl = This.AddOrGetControl( loEvent, lcObjPath )

		This.AddCode( loControl, lcCode, tnPrioridad )

		Bindevent( loObject, tcEvent, Thisform, 'HandlerEvent', 1 )

	Endif && PemStatus( loObject, tcEvent, 5 )

Catch To oErr
	llOk = .F.
	loError = This.oError
	loError.Ctracelogin = loError.Ctracelogin + CR ;
		+ 'tcObject: ' + tcObject + CR ;
		+ 'tcEvent: ' + tcEvent + CR ;
		+ 'tcCode: ' + tcCode

	This.cXMLoError = loError.Process( oErr )
	Throw loError

Finally
	loError = This.oError
	loError.Cremark = ''
	loError.Ctracelogin = ''
	loError = Null

Endtry

Return llOk

ENDPROC
PROCEDURE addobjectto
Lparameters tcControlName As String, ;
	tcObjParent As String, ;
	tcObjClass As String, ;
	tcObjClassLibrary As String, ;
	tcProperties As String, ;
	tcCursorAlias As String

#INCLUDE "FW\Comunes\Include\Praxis.h"

Local loObjParent As Object
Local lcAlias As String
Local lcProperties As String
Local lcObjParent As String
Local lcObjClass As String
Local lcObjClassLibrary As String
Local lcProperties As String
Local loError As ErrorHandler Of "Tools\ErrorHandler\Prg\ErrorHandler.prg"

Try
	lcAlias = Alias()

	If ! Empty( tcObjParent )
		tcObjParent = Alltrim( tcObjParent )
		loObjParent = This.GetRef( tcObjParent )

		If Vartype( loObjParent ) # 'O'
			Select Alias( tcCursorAlias )
			TEXT To lcCommand NoShow TextMerge Pretext 15
				Locate for Lower( Alltrim( cObjName ) ) = '<< Lower( tcObjParent  )>>'
			ENDTEXT

			&lcCommand

			lcControlName = Evaluate( tcCursorAlias + '.cObjName' )
			lcObjParent = Evaluate( tcCursorAlias + '.cParent' )
			lcObjClass = Evaluate( tcCursorAlias + '.cClass' )
			lcObjClassLibrary = Evaluate( tcCursorAlias + '.cClassLib' )
			lcProperties = Evaluate( tcCursorAlias + '.cProps' )

			This.AddObjectTo( lcControlName, lcObjParent, lcObjClass, lcObjClassLibrary, lcProperties, tcCursorAlias )

			loObjParent = This.GetRef( tcObjParent )

		Endif && Vartype( loObjParent ) # 'O'

	Else
		loObjParent = Thisform

	Endif && ! Empty( lcObjParent )

	If ! Empty( tcObjClassLibrary )
		tcObjClassLibrary = Lower( Alltrim( tcObjClassLibrary ) )
		If  ! ( tcObjClassLibrary $ Lower( Set("Classlib" ) ) ) Or ! ( tcObjClassLibrary $ Lower( Set("Procedure") ) )

			Do Case
				Case Inlist( Justext( tcObjClassLibrary ), 'prg', 'fxp' )

					TEXT To lcCommand NoShow TextMerge Pretext 15
						Set Procedure To '<<tcObjClassLibrary>>' Additive

					ENDTEXT

				Case Inlist( Justext( tcObjClassLibrary ), 'vcx' )
					TEXT To lcCommand NoShow TextMerge Pretext 15
						Set Classlib To '<<tcObjClassLibrary>>' Additive

					ENDTEXT

				Otherwise
					Error 'Tipo de archivo invalido para ClassLibrary'

			Endcase

			&lcCommand

		Endif && ! ( tcObjClassLibrary $ Lower( Set("Classlib" ) ) ) Or ! ( tcObjClassLibrary $ Lower( Set("Procedure") ) )

	Endif && ! Empty( tcObjClassLibrary )

	tcControlName = Alltrim( tcControlName )
	tcObjClass = Alltrim( tcObjClass )

	Assert Vartype( loObjParent ) = 'O' Message 'loObjParent noes un Objeto'

	loObjParent.AddObject( tcControlName, tcObjClass )

	loObj = loObjParent.&tcControlName.
	TEXT To lcCommand NoShow TextMerge Pretext 15
		Replace Loaded with .T.
		For Lower( Alltrim( cObjName ) ) == '<<Lower( Alltrim( tcControlName ) )>>'
		And Lower( Alltrim( cParent ) ) == '<<Lower( Alltrim( tcObjParent ) )>>'
		in <<tcCursorAlias>>
	ENDTEXT

	&lcCommand

	This.ApplyPropertiesTo( loObj, tcProperties )

Catch To oErr
	loError = Thisform.oError
	loError.Ctracelogin = loError.Ctracelogin + CR ;
		+ 'tcControlName: ' + Transform( tcControlName ) + CR ;
		+ 'tcObjParent: ' + Transform( tcObjParent ) + CR ;
		+ 'tcObjClass: ' + Transform( tcObjClass ) + CR ;
		+ 'tcObjClassLibrary: ' + Transform( tcObjClassLibrary ) + CR ;
		+ 'tcProperties: ' + Transform( tcProperties ) + CR ;
		+ 'tcCursorAlias: ' + Transform( tcCursorAlias )
		
	Thisform.cXMLoError = loError.Process( oErr )
	Throw loError

Finally
	loError = Thisform.oError
	loError.Cremark = ''
	loError.Ctracelogin = ''
	loError = Null
	
		loObj = Null
	If Used( lcAlias )
		Select Alias( lcAlias )

	Endif && Used( lcAlias )


Endtry

ENDPROC
PROCEDURE addorgetcontrol
Lparameters toEvent As Object, tcControl As String

Local loControl As PrxCollection Of "Fw\TierAdapter\Comun\PrxBaseLibrary.prg"
Local i As Integer
Local lcKey As String
Local loError As ErrorHandler Of "Tools\ErrorHandler\Prg\ErrorHandler.prg"

Try
	lcKey = Lower( tcControl )
	i = toEvent.GetKey( lcKey )
	If Empty( i )
		loControl = Newobject( 'PrxCollection', 'Fw\TierAdapter\Comun\PrxBaseLibrary.prg' )
		toEvent.Add( loControl, lcKey )

	Else
		loControl = toEvent.Item( i )

	Endif && Empty( i )

Catch To oErr
	loError = This.oError
	This.cXMLoError = loError.Process( oErr )
	Throw loError

Finally
	loError = This.oError
	loError.Cremark = ''
	loError.Ctracelogin = ''
	loError = Null

Endtry

Return loControl

ENDPROC
PROCEDURE addorgetevent
Lparameters tcEvent As String

Local loEvent As PrxCollection Of "Fw\TierAdapter\Comun\PrxBaseLibrary.prg"
Local i As Integer
Local lcKey As String
Local loErr As Exception
Local loError As ErrorHandler Of "Tools\ErrorHandler\Prg\ErrorHandler.prg"

Try

	lcKey = Lower( tcEvent )
	i = This.oColEvents.GetKey( lcKey )
	If Empty( i )
		loEvent = Newobject( 'PrxCollection', "Fw\TierAdapter\Comun\PrxBaseLibrary.prg" )
		This.oColEvents.Add( loEvent, lcKey )

	Else
		loEvent = This.oColEvents.Item( i )

	Endif && Empty( i )

Catch To oErr
	loError = Thisform.oError
	Thisform.cXMLoError = loError.Process( oErr )
	Throw loError

Finally
	loError = Thisform.oError
	loError.Cremark = ''
	loError.Ctracelogin = ''
	loError = Null

Endtry

Return loEvent

ENDPROC
PROCEDURE applypropertiesto
Lparameters toObj As Object, tcProperties As String

#INCLUDE "FW\Comunes\Include\Praxis.h"

Local loError As ErrorHandler Of "Tools\ErrorHandler\Prg\ErrorHandler.prg"
Local Array laLines[ 1 ]
Local lnCnt As Integer

Try
	lnCnt = Alines( laLines, tcProperties, 1 + 4 )

	If lnCnt > 0
		For i = 1 To lnCnt
			This.ApplyProperty( toObj, laLines[ i ] )

		Endfor
	Else
		This.ApplyProperty( toObj, tcProperties )

	Endif && lnCnt > 0

Catch To oErr
	loError = Thisform.oError
	loError.Ctracelogin = loError.Ctracelogin + CR ;
		+ 'toObj: ' + toObj.Name + CR ;
		+ 'tcProperties: ' + tcProperties
	Thisform.cXMLoError = loError.Process( oErr )
	Throw loError

Finally
	loError = Thisform.oError
	loError.Cremark = ''
	loError.Ctracelogin = ''
	loError = Null

Endtry

ENDPROC
PROCEDURE applyproperty
Lparameters toObj As Object, tcProperty As String

#INCLUDE "FW\Comunes\Include\Praxis.h"

Local lcProp As String
Local lcValue As String
Local lvValue As variant
Local loError As ErrorHandler Of "Tools\ErrorHandler\Prg\ErrorHandler.prg"

Try
	lcProp = Alltrim( Getwordnum( tcProperty, 1, '=' ) )
	lcValue = Alltrim( Getwordnum( tcProperty, 2, '=' ) )
	lvValue = &lcValue

	If Pemstatus( toObj, lcProp, 5 )
		toObj.&lcProp. = lvValue

	Else
		Error 'No existe la propiedad ' + lcProp + ' en el objeto ' + toObj.Name

	Endif && Pemstatus( toObj, lcProp, 5 )

Catch To oErr
	loError = Thisform.oError
	loError.Ctracelogin = loError.Ctracelogin + CR ;
		+ 'toObj: ' + toObj.Name + CR ;
		+ 'tcProperty: ' + tcProperty

	Thisform.cXMLoError = loError.Process( oErr )
	Throw loError

Finally
	loError = Thisform.oError
	loError.Cremark = ''
	loError.Ctracelogin = ''
	loError = Null

Endtry

ENDPROC
PROCEDURE classbeforeinit
Lparameters toParam As Object

Local loColTables As ColTables Of "Tools\Sincronizador\colDataBases.prg"
Local loCol As PrxCollection Of "Fw\TierAdapter\Comun\PrxBaseLibrary.prg"
Local loColFields As PrxCollection Of "Fw\TierAdapter\Comun\PrxBaseLibrary.prg"
Local loColHijos As PrxCollection Of "Fw\TierAdapter\Comun\PrxBaseLibrary.prg"
Local loTable As oTable Of "Tools\Sincronizador\colDataBases.prg"
Local loError As ErrorHandler Of "Tools\ErrorHandler\Prg\ErrorHandler.prg"
Local loDatosEntidad As ccmarco Of controles_varios.vcx
Local loField As oField Of "Tools\Sincronizador\ColDataBases.prg"
Local loIB As IBContainer Of InputBoxes.vcx
Local loTableChild As oTable Of "Tools\Sincronizador\ColDataBases.prg"
Local loPageframe As prxPageFrame Of prxbase.vcx
Local loPage As prxPage Of prxbase.vcx
Local loABMChild As ABMChildContainer Of prxmainform.vcx
Local i As Integer
Local lcWhere As String

Try
	loError = Thisform.oError
	loError.Ctracelogin = 'Armando ' + This.cDataConfigurationKey

	* @TODO Damian Eiff 2009-08-18 (01:45:10)
	* Implementar la creación de los controles dinamicamente desde el sincronizador
	loColTables = NewcolTables()

	This.Caption = Printf( 'ABM %s', CamelProperCase( This.cDataConfigurationKey ) )
	loError.Ctracelogin = loError.Ctracelogin + Chr( 13 ) + This.Caption
	
	lcWhere = 'Lower(padre)=' + Any2Char( Lower( This.cDataConfigurationKey ), .T. )
	
	loError.Ctracelogin = loError.Ctracelogin + Chr( 13 ) + 'Filtro: ' + lcWhere
	loColHijos = loColTables.Where ( lcWhere )

	loTable = loColTables.GetItem( This.cDataConfigurationKey )
	loColFields = loTable.oColFields.Where ( ' (lIsSystem = .F.) Or (Lower(Name) == "default") ' )
	lnFieldsCount = loColFields.Count
	lnTabIndex = 1

	If lnFieldsCount > 0
		lcContainerName = 'DatosEntidad' + loTable.Name
		This.SetClassLib( 'controles_varios.vcx' )

		This.AddObject( lcContainerName, 'ccmarco' )
		loDatosEntidad = This.&lcContainerName.
		loDatosEntidad.Visible = .T.
		loDatosEntidad.TabIndex = lnTabIndex
		loDatosEntidad.nFitMode = 5
		loDatosEntidad.lFitToParent = .T.
		loDatosEntidad.lblTitulo.Visible = .F.
		loDatosEntidad.nTopPadding = loDatosEntidad.nBottomPadding
		lnTabIndex = lnTabIndex + 1

		For i = 1 To lnFieldsCount
			loField = loColFields.Item[ i ]
			llOk1 = Empty( loField.References ) Or Lower( loField.References ) # Lower( loTable.Padre )
			llOk2 = Empty( loTable.MainID ) Or Lower( loField.Name ) # Lower( loTable.MainID )

			If llOk1 And llOk2
				lcControlName = 'cnt' + loField.Name
				This.SetClassLib( loField.DisplayClassLibrary )

				loDatosEntidad.AddObject( lcControlName, loField.DisplayClass )
				loIB = loDatosEntidad.&lcControlName.
				loIB.TabIndex = i
				loIB.Visible = .T.
				This.CloneObj( loIB, loField )
				loIB.cFieldName = loField.Name
				loIB.cKeyName = Lower( This.cDataConfigurationKey + '.' + loField.Name )

			Endif

		Endfor

	Endif

	lnHijosCount = loColHijos.Count
	Do Case
		Case lnHijosCount = 1
			loTableChild = loColHijos.Item[ 1 ]

			lcContainerName = 'ABM' + loTableChild.Name
			If loTableChild.lIsHierarchical
				lcContainerClass = 'ABMChildTree'

			Else
				lcContainerClass  = 'ABMChildGrid'

			Endif

			This.SetClassLib( 'PrxMainForm.vcx' )
			This.AddObject( lcContainerName, lcContainerClass )
			loABMChild = This.&lcContainerName.

			loABMChild.Visible = .T.
			loABMChild.nFitMode = 5
			loABMChild.lFitToParent = .T.
			loABMChild.cDataConfigurationKey = loTableChild.Name
			loABMChild.TabIndex = lnTabIndex
			loABMChild.lblTitulo.Caption = CamelProperCase( loTableChild.Name )
			lnTabIndex = lnTabIndex + 1

		Case lnHijosCount > 1
			lcPageFrameName = 'pgHijos'
			This.SetClassLib( 'prxbase.vcx' )
			This.AddObject( lcPageFrameName, 'prxpageframe' )
			loPageframe = This.&lcPageFrameName.
			loPageframe.Visible = .T.
			loPageframe.TabIndex = lnTabIndex
			lnTabIndex = lnTabIndex + 1

			* FW
			loPageframe.nFitMode = 5
			loPageframe.lFitToParent = .T.
			loPageframe.PageCount = lnHijosCount
			loPageframe.Tabs = .T.

			This.SetClassLib( 'PrxMainForm.vcx' )
			For i = 1 To lnHijosCount
				loTableChild = loColHijos.Item[ i ]
				lcContainerName = 'ABM' + loTableChild.Name
				If loTableChild.lIsHierarchical
					lcContainerClass = 'ABMChildTree'

				Else
					lcContainerClass  = 'ABMChildGrid'

				Endif

				loPage = loPageframe.Pages[ i ]
				loPage.AddObject( lcContainerName, lcContainerClass )
				loABMChild = loPage.&lcContainerName.

				loABMChild.Visible = .T.
				loABMChild.nFitMode = 15
				loABMChild.lFitToParent = .T.
				loABMChild.cDataConfigurationKey = loTableChild.Name
				loABMChild.TabIndex = 1
				loABMChild.lblTitulo.Caption = CamelProperCase( loTableChild.Name )

			Endfor

		Otherwise

	Endcase

	This.Navigator.TabIndex = lnTabIndex
	lnTabIndex = lnTabIndex + 1

	This.Optionpanel.TabIndex = lnTabIndex

Catch To oErr
	loError = Thisform.oError
	Thisform.cXMLoError = loError.Process( oErr )
	Throw loError

Finally
	loError = Thisform.oError
	loError.Cremark = ''
	loError.Ctracelogin = ''

	loColTables = Null
	loCol = Null
	loColFields = Null
	loColHijos = Null
	loTable = Null
	loError = Null
	loDatosEntidad = Null
	loField = Null
	loIB = Null
	loTableChild = Null
	loPageframe = Null
	loPage = Null
	loABMChild = Null

Endtry


Return DoDefault( toParam )

ENDPROC
PROCEDURE clearbindevents
Local loColEvents As Collection
Local loEvent As Collection
Local loControl As Collection
Local loObj As Object
Local lcControl As String
Local lcEvent As String

This.lOnExit = .T.

loColEvents = This.oColEvents
For i = loColEvents.Count To 1 Step - 1
	loEvent = loColEvents.Item( i )
	lcEvent = loColEvents.GetKey( i )

	For j = loEvent.Count To 1 Step - 1
		loControl = loEvent.Item( j )
		lcControl = loEvent.GetKey( j )

		* Obtengo la referencia al control
		loObj = &lcControl.
		* Elimino el BindEvents
		Unbindevents( loObj, lcEvent, Thisform, 'HandlerEvent' )

		* Elimino los codigos asociados al evento y al control
		loControl.Remove( - 1 )

		* Elimino el control del evento
		loEvent.Remove( j )

	Endfor

	* Elimino el evento
	loColEvents.Remove( i )

Endfor

This.oColEvents.Remove( - 1 )

Unbindevents( This )

ENDPROC
PROCEDURE cloneobj
Lparameters toDest As Object, toSource As Object

Local Array laMembers[ 1 ]
Local lcProp As String
Local i as Integer

If Vartype( toDest ) == "O" And Vartype( toSource ) == "O"
	For i = 1 To Amembers( laMembers, toSource )
		lcProp = laMembers[ i ]

		Try
			toDest.&lcProp. = toSource.&lcProp.

		Catch To oErr
		Endtry

	Endfor
	If Pemstatus( toSource, 'Class', 5 )
		For i = 1 To Amembers( laMembers, toSource.Class )
			lcProp = laMembers[ i ]

			Try
				toDest.&lcProp. = toSource.&lcProp.

			Catch To oErr
			Endtry

		Endfor

	EndIf && Pemstatus( toSource, 'Class', 5 )

Endif

ENDPROC
PROCEDURE cmdDebugInfo.RightClick

Local lnOpcion As Number
Local lcRet As String
Local loError As ErrorHandler Of "Tools\ErrorHandler\Prg\ErrorHandler.prg"
Try
	loError = Thisform.oError
	loError.Cremark = ''
	loError.Ctracelogin = ''

	lnOpcion = xmenu( 'Ver Bordes;Ocultar Bordes;Ver Propiedades;Ver Propiedades Nativas;Cambiar BorderStyle' )

	Do Case
		Case lnOpcion = 1
			VerBordes( Thisform )

		Case lnOpcion = 2
			OCultarBordes( Thisform )

		Case lnOpcion = 3
			lcRet = VerProperties( Thisform  )
			_Cliptext = lcRet

		Case lnOpcion = 4
			lcRet = VerProperties( Thisform, 0, .T. )
			_Cliptext = lcRet

		Case lnOpcion = 5
			Thisform.BorderStyle= 3

		Otherwise

	Endcase

Catch To oErr
	loError = Thisform.oError
	Thisform.cXMLoError = loError.Process( oErr )
	Throw loError

Finally
	loError = Thisform.oError
	loError.Cremark = ''
	loError.Ctracelogin = ''
	loError = Null

Endtry

ENDPROCPROCEDURE filldefaultparameters
Local loCol As PrxCollection Of "Fw\TierAdapter\Comun\PrxBaseLibrary.prg"
Local oErr As Exception
Local loError As ErrorHandler Of "Tools\ErrorHandler\Prg\ErrorHandler.prg"

Try

	With This As oForm Of Pruebas\EditFrm.prg

		* Drag Events
		* DragDrop

		loCol = Newobject( 'PrxCollection', "Fw\TierAdapter\Comun\PrxBaseLibrary.prg" )
		loCol.Add( 'oSource' )
		loCol.Add( 'nXCoord' )
		loCol.Add( 'nYCoord' )
		This.oColDefaultParameters.Add( loCol, 'dragdrop' )

		* DragOver
		loCol = Newobject( 'PrxCollection', "Fw\TierAdapter\Comun\PrxBaseLibrary.prg" )
		loCol.Add( 'oSource' )
		loCol.Add( 'nXCoord' )
		loCol.Add( 'nYCoord' )
		loCol.Add( 'nState' )
		This.oColDefaultParameters.Add( loCol, 'dragover' )

		* Error
		loCol = Newobject( 'PrxCollection', "Fw\TierAdapter\Comun\PrxBaseLibrary.prg" )
		loCol.Add( 'nError' )
		loCol.Add( 'cMethod' )
		loCol.Add( 'nLine' )
		This.oColDefaultParameters.Add( loCol, 'error' )

		* KeyPress
		loCol = Newobject( 'PrxCollection', "Fw\TierAdapter\Comun\PrxBaseLibrary.prg" )
		loCol.Add( 'nKeyCode' )
		loCol.Add( 'nShiftAltCtrl' )
		This.oColDefaultParameters.Add( loCol, 'keypress' )

		* Mouse Events
		loCol = Newobject( 'PrxCollection', "Fw\TierAdapter\Comun\PrxBaseLibrary.prg" )
		loCol.Add( 'nButton' )
		loCol.Add( 'nShift' )
		loCol.Add( 'nXCoord' )
		loCol.Add( 'nYCoord' )
		This.oColDefaultParameters.Add( loCol, 'mousedown' )
		This.oColDefaultParameters.Add( loCol, 'mouseenter' )
		This.oColDefaultParameters.Add( loCol, 'mouseleave' )
		This.oColDefaultParameters.Add( loCol, 'mousemove' )
		This.oColDefaultParameters.Add( loCol, 'mouseup' )

		* Mouse Events : MouseWheel
		loCol = Newobject( 'PrxCollection', "Fw\TierAdapter\Comun\PrxBaseLibrary.prg" )
		loCol.Add( 'nDirection' )
		loCol.Add( 'nShift' )
		loCol.Add( 'nXCoord' )
		loCol.Add( 'nYCoord' )
		This.oColDefaultParameters.Add( loCol, 'mousewheel' )

		* OLE Events
		* OLECompleteDrag
		loCol = Newobject( 'PrxCollection', "Fw\TierAdapter\Comun\PrxBaseLibrary.prg" )
		loCol.Add( 'nEffect' )
		This.oColDefaultParameters.Add( loCol, 'olecompletedrag' )

		* OLEDragDrop
		loCol = Newobject( 'PrxCollection', "Fw\TierAdapter\Comun\PrxBaseLibrary.prg" )
		loCol.Add( 'oDataObject' )
		loCol.Add( 'nEffect' )
		loCol.Add( 'nButton' )
		loCol.Add( 'nShift' )
		loCol.Add( 'nXCoord' )
		loCol.Add( 'nYCoord' )
		This.oColDefaultParameters.Add( loCol, 'oledragdrop' )

		* OLEDragOver
		loCol = Newobject( 'PrxCollection', "Fw\TierAdapter\Comun\PrxBaseLibrary.prg" )
		loCol.Add( 'oDataObject' )
		loCol.Add( 'nEffect' )
		loCol.Add( 'nButton' )
		loCol.Add( 'nShift' )
		loCol.Add( 'nXCoord' )
		loCol.Add( 'nYCoord' )
		loCol.Add( 'nState' )
		This.oColDefaultParameters.Add( loCol, 'oledragover' )

		* OLEGiveFeedback
		loCol = Newobject( 'PrxCollection', "Fw\TierAdapter\Comun\PrxBaseLibrary.prg" )
		loCol.Add( 'nEffect' )
		loCol.Add( 'eMouseCursor' )
		This.oColDefaultParameters.Add( loCol, 'olegivefeedback' )

		* OLESetData
		loCol = Newobject( 'PrxCollection', "Fw\TierAdapter\Comun\PrxBaseLibrary.prg" )
		loCol.Add( 'oDataObject' )
		loCol.Add( 'eFormat' )
		This.oColDefaultParameters.Add( loCol, 'olesetdata' )

		* OLEStartDrag
		loCol = Newobject( 'PrxCollection', "Fw\TierAdapter\Comun\PrxBaseLibrary.prg" )
		loCol.Add( 'oDataObject' )
		loCol.Add( 'nEffect' )
		This.oColDefaultParameters.Add( loCol, 'olestartdrag' )

		* OnMoveItem
		loCol = Newobject( 'PrxCollection', "Fw\TierAdapter\Comun\PrxBaseLibrary.prg" )
		loCol.Add( 'nSource' )
		loCol.Add( 'nShift' )
		loCol.Add( 'nCurrentIndex' )
		loCol.Add( 'nMoveBy' )
		This.oColDefaultParameters.Add( loCol, 'onmoveitem' )

		* Scrolled
		loCol = Newobject( 'PrxCollection', "Fw\TierAdapter\Comun\PrxBaseLibrary.prg" )
		loCol.Add( 'nDirection' )
		This.oColDefaultParameters.Add( loCol, 'scrolled' )

		* UIEnable
		loCol = Newobject( 'PrxCollection', "Fw\TierAdapter\Comun\PrxBaseLibrary.prg" )
		loCol.Add( 'lEnable' )
		This.oColDefaultParameters.Add( loCol, 'uienable' )

	Endwith

Catch To oErr
	loError = Thisform.oError
	Thisform.cXMLoError = loError.Process( oErr )
	Throw loError

Finally
	loError = Thisform.oError
	loError.Cremark = ''
	loError.Ctracelogin = ''
	loError = Null

	loCol = Null

Endtry

ENDPROC
PROCEDURE getpath
Lparameters toObj As Object

#INCLUDE "FW\Comunes\Include\Praxis.h"

Local lcRet As String
Local loObj As Object
Local loError As ErrorHandler Of "Tools\ErrorHandler\Prg\ErrorHandler.prg"

Try

	lcRet = ''

	If Vartype( toObj ) = 'O'

		If ! Pemstatus( toObj, 'CompleteObjectPath', 5 )
			Do While ! Isnull( toObj ) And Lower( toObj.BaseClass ) # 'form'
				lcRet = toObj.Name + '.' + lcRet
				toObj = toObj.Parent

			Enddo
			If ! Empty( lcRet )
				lcRet = 'ThisForm.' + lcRet
				If Right( lcRet, 1 ) = '.'
					lcRet = Left( lcRet, Len( lcRet ) - 1 )

				Endif && Right( lcRet, 1 ) = '.'

			Else
				lcRet = 'ThisForm'

			Endif && ! Empty( lcRet )

			AddProperty( toObj, 'CompleteObjectPath', lcRet )

		Else
			lcRet = toObj.CompleteObjectPath

		Endif && ! Pemstatus( toObj, 'CompleteObjectPath', 5 )

	Endif && Vartype( toObj ) = 'O'

Catch To oErr
	loError = Thisform.oError
	loError.Ctracelogin = loError.Ctracelogin + CR ;
		+ 'toObj: ' + toObj.Name
	Thisform.cXMLoError = loError.Process( oErr )
	Throw loError

Finally
	loError = Thisform.oError
	loError.Cremark = ''
	loError.Ctracelogin = ''
	loError = Null

Endtry

Return lcRet

ENDPROC
PROCEDURE getref
Lparameters tcObj As String

#INCLUDE "FW\Comunes\Include\Praxis.h"

Local loObj As Object
Local loError As ErrorHandler Of "Tools\ErrorHandler\Prg\ErrorHandler.prg"

Try
	tcObj = Alltrim( tcObj )

	If Atc( '.', tcObj ) > 0
		loObj = &tcObj

	Else
		If Inlist( Lower( tcObj ), 'this', 'thisform' )
			loObj = This

		Else
			loObj = This.GetRefR( tcObj, This )

		Endif && Inlist( Lower( tcObj ), 'this', 'thisform' )

	Endif && Atc( '.', tcObj ) > 0

Catch To oErr
	loError = Thisform.oError
	loError.Ctracelogin = 	loError.Ctracelogin + CR ;
		+ 'tcObj: ' + tcObj

	Thisform.cXMLoError = loError.Process( oErr )
	Throw loError

Finally
	loError = Thisform.oError
	loError.Cremark = ''
	loError.Ctracelogin = ''
	loError = Null

Endtry

Return loObj





ENDPROC
PROCEDURE getrefr
Lparameters tcObj As String, toObj As Object

#INCLUDE "FW\Comunes\Include\Praxis.h"

Local loObj As Object
Local lnCnt As Number
Local loErr As Exception
Local lcBaseClass As String
Local loError As ErrorHandler Of "Tools\ErrorHandler\Prg\ErrorHandler.prg"

Try

	loObj = Null
	tcObj = Alltrim( tcObj )

	If Vartype( toObj ) = 'O'
		lcBaseClass = Lower( toObj.BaseClass )
		Do Case
			Case Inlist( lcBaseClass, 'form', 'page', 'container' )
				lnCnt = 1
				Do While Vartype( loObj ) # 'O' And lnCnt <= toObj.Objects.Count
					loControl = toObj.Objects[ lnCnt ]
					If Pemstatus( loControl, 'Name', 5 ) And Lower( loControl.Name ) = Lower( tcObj )
						loObj = loControl

					Else
						loObj = This.GetRefR( tcObj, loControl )

					Endif && Pemstatus( loControl, 'Name', 5 ) And Lower( loControl.Name ) = Lower( tcObj )
					lnCnt = lnCnt + 1

				Enddo

			Case lcBaseClass = 'pageframe'
				lnCnt = 1
				Do While Vartype( loObj ) # 'O' And lnCnt <= toObj.Pages.Count
					loControl = toObj.Pages[ lnCnt ]
					If Pemstatus( loControl, 'Name', 5 ) And Lower( loControl.Name ) = Lower( tcObj )
						loObj = loControl

					Else
						loObj = This.GetRefR( tcObj, loControl )

					Endif && Pemstatus( loControl, 'Name', 5 ) And Lower( loControhl.Name ) = Lower( tcObj )
					lnCnt = lnCnt + 1

				Enddo

			Case lcBaseClass = 'grid'
				lnCnt = 1
				Do While Vartype( loObj ) # 'O' And lnCnt <= toObj.Columns.Count
					loControl = toObj.Columns[ lnCnt ]
					If Pemstatus( loControl, 'Name', 5 ) And Lower( loControl.Name ) = Lower( tcObj )
						loObj = loControl

					Else
						loObj = This.GetRefR( tcObj, loControl )

					Endif
					lnCnt = lnCnt + 1

				Enddo

			Otherwise

				If Pemstatus( toObj, 'Controls', 5 )
					lnCnt = 1
					Do While Vartype( loObj ) # 'O' And lnCnt <= toObj.Controls.Count
						loControl = toObj.Controls[ lnCnt ]
						If Pemstatus( loControl, 'Name', 5 ) And Lower( loControl.Name ) = Lower( tcObj )
							loObj = loControl

						Else
							loObj = This.GetRefR( tcObj, loControl )

						Endif && Pemstatus( loControl, 'Name', 5 ) And Lower( loControl.Name ) = Lower( tcObj )
						lnCnt = lnCnt + 1

					Enddo

				Endif && Pemstatus( toObj, 'Controls', 5 )

				If Pemstatus( toObj, 'Objects', 5 )
					lnCnt = 1
					Do While Vartype( loObj ) # 'O' And lnCnt <= toObj.Objects.Count
						loControl = toObj.Objects[ lnCnt ]
						If Pemstatus( loControl, 'Name', 5 ) And Lower( loControl.Name ) = Lower( tcObj )
							loObj = loControl

						Else
							loObj = This.GetRefR( tcObj, loControl )

						Endif && Pemstatus( loControl, 'Name', 5 ) And Lower( loControl.Name ) = Lower( tcObj )
						lnCnt = lnCnt + 1

					Enddo

				Endif && Pemstatus( toObj, 'Objects', 5 )

		Endcase

	Endif && Vartype( toObj ) = 'O'

Catch To oErr
	loError = Thisform.oError
	loError.Ctracelogin = loError.Ctracelogin + CR ;
		+ 'tcObj: ' + tcObj + CR ;
		+ 'toObj: ' + Iif( Vartype( toObj ) = 'O', toObj.Name, '' )

	Thisform.cXMLoError = loError.Process( oErr )
	Throw loError

Finally
	loError = Thisform.oError
	loError.Cremark = ''
	loError.Ctracelogin = ''
	loError = Null

Endtry

Return loObj

ENDPROC
PROCEDURE handlerevent
Lparameters tv1 As variant, tv2 As variant, tv3 As variant, ;
	tv4 As variant, tv5 As variant, tv6 As variant, tv7 As variant, ;
	tv8 As variant, tv9 As variant, tv10 As variant, tv11 As variant, ;
	tv12 As variant, tv13 As variant, tv14 As variant, tv15 As variant, ;
	tv16 As variant, tv17 As variant, tv18 As variant, tv19 As variant, ;
	tv20 As variant, tv21 As variant, tv22 As variant, tv23 As variant, ;
	tv24 As variant, tv25 As variant, tv26 As variant


Local lcCode As String
Local lcEvent As String
Local loSender As Object
Local i As Integer
Local Array laEvents[ 1 ]
Local lnEvtCnt As Number
Local loParam As Object
Local lcCreateParam As String
Local lnPcount As Number
Local oErr As Exception
Local lcMessage As String
Local loError As ErrorHandler Of "Tools\ErrorHandler\Prg\ErrorHandler.prg"

Try
	If ! This.lOnExit
		lnEvtCnt = Aevents( laEvents, 0 )
		lcCode = ''
		lcCreateParam = ''

		If ! Empty( lnEvtCnt )

			lcEvent = Lower( laEvents[ 1, 2 ] )

			i =	Thisform.oColEvents.GetKey( lcEvent )

			If ! Empty( i )
				loEvent = Thisform.oColEvents.Item( i )
				* Obtendo el Origen del evento
				loSender = laEvents[ 1, 1 ]
				lcObjPath = This.GetPath( loSender )
				j = loEvent.GetKey( Lower( lcObjPath ) )
				If ! Empty( j )
					loControl = loEvent.Item( j )

					l = This.oColDefaultParameters.GetKey( lcEvent )
					If ! Empty( l )
						loEventDeFaultParam = This.oColDefaultParameters.Item( l )

					Endif

					lnPcount = Pcount()

					If lnPcount > 0
						lcCreateParam = ''
						For i = 1 To lnPcount

							lcCreateParam = lcCreateParam ;
								+ '"Param' + Transform( i ) + '", ' ;
								+ 'tv' + Transform( i )

							If Vartype( loEventDeFaultParam ) = 'O' And i <= loEventDeFaultParam.Count

								lcEventDeFaultParamName = loEventDeFaultParam.Item( i )
								lcCreateParam = lcCreateParam ;
									+ ', "' + lcEventDeFaultParamName + '", ' ;
									+ 'tv' + Transform( i )

							Endif && Vartype( loEventDeFaultParam ) = 'O' And loEventDeFaultParam.Count <= i

							If i # lnPcount
								lcCreateParam = lcCreateParam + ','

							Endif

						Endfor

					Endif && lnPcount > 0

					If ! Empty( lcCreateParam )
						lcCreateParam = 'CreateObjParam(' + lcCreateParam + ')'
						*
						loParam = &lcCreateParam

					Endif && ! Empty( lcCreateParam )

					For k = 1 To loControl.Count
						lcCode = loControl.Item( k )
						If ! Empty( lcCode )
							If Empty( lcCreateParam )
								Execscript( lcCode, Thisform, loSender )

							Else
								Execscript( lcCode, Thisform, loSender, loParam )

							Endif && Empty( lcCreateParam )

						Endif && ! Empty( lcCode )

					Endfor

				Endif && ! Empty( j )

			Endif && ! Empty( i )

		EndIf
		
	Endif && ! this.lOnExit

Catch To oErr
	loError = Thisform.oError
	loError.Ctracelogin = loError.Ctracelogin + CR ;
		+ 'tv1: ' + Transform( tv1 ) + CR ;
		+ 'tv2: ' + Transform( tv2 ) + CR ;
		+ 'tv3: ' + Transform( tv3 ) + CR ;
		+ 'tv4: ' + Transform( tv4 ) + CR ;
		+ 'tv5: ' + Transform( tv5 ) + CR ;
		+ 'tv6: ' + Transform( tv6 ) + CR ;
		+ 'tv7: ' + Transform( tv7 ) + CR ;
		+ 'tv8: ' + Transform( tv8 ) + CR ;
		+ 'tv9: ' + Transform( tv9 ) + CR ;
		+ 'tv10: ' + Transform( tv10 ) + CR ;
		+ 'tv11: ' + Transform( tv11 ) + CR ;
		+ 'tv12: ' + Transform( tv12 ) + CR ;
		+ 'tv13: ' + Transform( tv13 ) + CR ;
		+ 'tv14: ' + Transform( tv14 ) + CR ;
		+ 'tv15: ' + Transform( tv15 ) + CR ;
		+ 'tv16: ' + Transform( tv16 ) + CR ;
		+ 'tv17: ' + Transform( tv17 ) + CR ;
		+ 'tv18: ' + Transform( tv18 ) + CR ;
		+ 'tv19: ' + Transform( tv19 ) + CR ;
		+ 'tv20: ' + Transform( tv20 ) + CR ;
		+ 'tv21: ' + Transform( tv21 ) + CR ;
		+ 'tv22: ' + Transform( tv22 ) + CR ;
		+ 'tv23: ' + Transform( tv23 ) + CR ;
		+ 'tv24: ' + Transform( tv24 ) + CR ;
		+ 'tv25: ' + Transform( tv25 ) + CR ;
		+ 'tv26: ' + Transform( tv26 )
	Thisform.cXMLoError = loError.Process( oErr )
	Throw loError

Finally
	loError = Thisform.oError
	loError.Cremark = ''
	loError.Ctracelogin = ''
	loError = Null

Endtry

Return

ENDPROC
PROCEDURE loadconfig
Lparameters tcFile As String

#INCLUDE "FW\Comunes\Include\Praxis.h"

Local loError As ErrorHandler Of "Tools\ErrorHandler\Prg\ErrorHandler.prg"
Local loXA As Xmladapter
Local loErr As Exception
Local lcExt As String
Local lcTables As String
Local lcAlias As String
Local lcMessage As String
Local loTable As Xmltable

Try

	lcTables = ''
	lcAlias = Alias()
	If ! Empty( tcFile )
		lcExt = Lower( Justext( tcFile ) )
		Do Case
			Case Inlist( lcExt, 'xml', 'xcfg' )
				loXA = Createobject( 'Xmladapter' )
				loXA.LoadXML( tcFile, .T., .T. )

				lnCnt = loXA.Tables.Count
				For i = 1 To lnCnt
					loTable = loXA.Tables.Item( i )
					lcTables = lcTables + loTable.Alias
					Use In Select( loTable.Alias )
					loTable.ToCursor()
					If i # lnCnt
						lcTables = lcTables + ','

					Endif

				Endfor

				If Used( 'cfgControls' )
					This.LoadControls( 'cfgControls' )

				Endif && Used( 'cfgControls' )

				If Used( 'cfgProperties' )
					This.LoadProperties( 'cfgProperties' )

				Endif && Used( 'cfgProperties' )

				If Used( 'cfgMethods' )
					This.LoadMethods( 'cfgMethods' )

				Endif && Used( 'cfgMethods' )

			Case Inlist( lcExt, 'dbf', 'dcfg' )
				* Error 'Funcionalidad no implementada'
				Use In Select( 'cfgConfig' )
				TEXT To lcCommand NoShow TextMerge Pretext 15
							Use '<<tcFile>>' In 0 Shared Again Alias cfgConfig

				ENDTEXT

				&lcCommand

				If Used( 'cfgConfig' )
					lcTables = 'cfgConfig'

					This.LoadControls( 'cfgConfig', 'cType = "C"' )
					This.LoadProperties( 'cfgConfig', 'cType = "P"' )
					This.LoadMethods( 'cfgConfig', 'cType = "M"' )

				Endif
			Otherwise
				Error 'Tipo de archivo desconocido'

		Endcase

	Endif

Catch To oErr
	loError = Thisform.oError
	loError.Ctracelogin = loError.Ctracelogin + CR ;
		+ 'tcFile: ' + Transform( tcFile )

	Thisform.cXMLoError = loError.Process( oErr )
	Throw loError

Finally
	loError = Thisform.oError
	loError.Cremark = ''
	loError.Ctracelogin = ''
	loError = Null
	If ! Empty( lcTables )

		lnCnt = Getwordcount( lcTables, ',' )
		For i = 1 To lnCnt
			lcTblAlias = Getwordnum( lcTables, i, ',' )
			Use In Select( lcTblAlias )

		Endfor

	Endif && ! Empty( lcTables )

	If Used( lcAlias )
		Select Alias( lcAlias )

	Endif && Used( lcAlias )

Endtry

ENDPROC
PROCEDURE loadcontrols
Lparameters tcCursorAlias As String, ;
	tcFilter As String

#INCLUDE "FW\Comunes\Include\Praxis.h"

Local loError As ErrorHandler Of "Tools\ErrorHandler\Prg\ErrorHandler.prg"
Local lcAlias As String
Local loErr As Exception
Local lcControlName As String
Local lcObjParent As String
Local lcObjClass As String
Local lcObjClassLibrary As String
Local lcProperties As String

Try
	lcAlias = Alias()
	If Used( tcCursorAlias )
		If Empty( tcFilter )
			tcFilter = '.T.'

		Endif && Empty( tcFilter )

		Select Alias( tcCursorAlias )
		Locate

		Scan For( &tcFilter ) ;
				And ! Deleted( tcCursorAlias ) ;
				And ! Evaluate( tcCursorAlias + '.Loaded' )
			lnRecNo = Recno( tcCursorAlias )

			lcControlName = Evaluate( tcCursorAlias + '.cObjName' )
			lcObjParent = Evaluate( tcCursorAlias + '.cParent' )
			lcObjClass = Evaluate( tcCursorAlias + '.cClass' )
			Set
			Set Step On
			lcObjClassLibrary = Evaluate( tcCursorAlias + '.cClassLib' )
			lcProperties = Evaluate( tcCursorAlias + '.cProps' )

			This.AddObjectTo( lcControlName, lcObjParent, lcObjClass, lcObjClassLibrary, lcProperties, tcCursorAlias )

			TEXT To lcCommand NoShow TextMerge Pretext 15
						Locate for Recno( tcCursorAlias ) = <<lnRecNo>>
			ENDTEXT

			&lcCommand

			If Len( Evaluate( tcCursorAlias + '.cEventCode' ) ) > 0
				This.LoadMethods( tcCursorAlias, Textmerge( "Recno( tcCursorAlias ) = <<lnRecNo>>" ) )

				&lcCommand

			Endif &&  Len( Evaluate( tcCursorAlias + '.cEventCode' ) ) > 0

		Endscan

	Endif && Used( tcCursorAlias )

Catch To oErr
	loError = Thisform.oError
	loError.Ctracelogin = loError.Ctracelogin + CR ;
		+ 'tcCursorAlias: ' + Transform( tcCursorAlias ) + CR ;
		+ 'tcFilter: ' + Transform( tcFilter )

	Thisform.cXMLoError = loError.Process( oErr )
	Throw loError

Finally
	loError = Thisform.oError
	loError.Cremark = ''
	loError.Ctracelogin = ''
	loError = Null
	loErr = Null

	If Used( lcAlias )
		Select Alias( lcAlias )

	Endif && Used( lcAlias )

Endtry



ENDPROC
PROCEDURE loadmethods
Lparameters tcCursorAlias As String, ;
	tcFilter As String

#INCLUDE "FW\Comunes\Include\Praxis.h"

Local loError As ErrorHandler Of "Tools\ErrorHandler\Prg\ErrorHandler.prg"
Local lcAlias As String
Local loErr As Exception
Local lcObjName As String
Local lcEventName As String
Local lcMethodCode As String
Local lnPriority As Integer

Try
	lcAlias = Alias()
	If Used( tcCursorAlias )
		If Empty( tcFilter )
			tcFilter = '.T.'

		Endif && Empty( tcFilter )

		Select Alias( tcCursorAlias )
		Locate

		Scan For( &tcFilter ) And ! Deleted( tcCursorAlias )

			lcObjName = Evaluate( tcCursorAlias + '.cObjName' )
			lcEventName = Evaluate( tcCursorAlias + '.cEventName' )
			lcMethodCode = Evaluate( tcCursorAlias + '.cEventCode' )
			lnPriority = Evaluate( tcCursorAlias + '.nPriority' )

			This.AddEventCode( lcObjName, lcEventName, lcMethodCode, lnPriority )

		Endscan

	Endif && Used( tcCursorAlias )

Catch To oErr
	loError = Thisform.oError
	loError.Ctracelogin = loError.Ctracelogin + CR ;
		+ 'tcCursorAlias: ' + tcCursorAlias + CR ;
		+ 'tcFilter: ' + tcFilter

	Thisform.cXMLoError = loError.Process( oErr )
	Throw loError

Finally
	loError = Thisform.oError
	loError.Cremark = ''
	loError.Ctracelogin = ''
	loError = Null

	If Used( lcAlias )
		Select Alias( lcAlias )

	Endif && Used( lcAlias )

Endtry


ENDPROC
PROCEDURE loadproperties
Lparameters tcCursorAlias As String, ;
	tcFilter As String

#INCLUDE "FW\Comunes\Include\Praxis.h"

Local loError As ErrorHandler Of "Tools\ErrorHandler\Prg\ErrorHandler.prg"
Local lcAlias As String
Local loErr As Exception
Local lcObjName As String
Local loObj As Object

Try
	lcAlias = Alias()
	If Used( tcCursorAlias )
		If Empty( tcFilter )
			tcFilter = '.T.'

		Endif && Empty( tcFilter )

		Select Alias( tcCursorAlias )
		Locate

		Scan For( &tcFilter ) And ! Deleted( tcCursorAlias )
			lcObjName = Evaluate( tcCursorAlias + '.cObjName' )
			lcProperties = Evaluate( tcCursorAlias + '.cProps' )
			loObj = This.GetRef( lcObjName )
			Assert Vartype( loObj ) = 'O' Message 'loObj no es un objeto'
			This.ApplyPropertiesTo( loObj, lcProperties )

		Endscan

	Endif && Used( tcCursorAlias )

Catch To oErr
	loError = Thisform.oError
	loError.Ctracelogin = loError.Ctracelogin + CR ;
		+ 'tcCursorAlias: ' + tcCursorAlias + CR ;
		+ 'tcFilter: ' + tcFilter

	Thisform.cXMLoError = loError.Process( oErr )
	Throw loError

Finally
	loError = Thisform.oError
	loError.Cremark = ''
	loError.Ctracelogin = ''
	loError = Null
	If Used( lcAlias )
		Select Alias( lcAlias )

	Endif && Used( lcAlias )

Endtry


ENDPROC
PROCEDURE ocoldefaultparameters_access

Local loError As ErrorHandler Of "Tools\ErrorHandler\Prg\ErrorHandler.prg"

Try
	With This
		If Vartype( .oColDefaultParameters ) # 'O'
			.oColDefaultParameters = Createobject( 'Collection' )

			.FillDefaultParameters()

		Endif && Vartype( .oColDefaultParameters ) # 'O'

	EndWith
	
Catch To oErr
	loError = Thisform.oError
	Thisform.cXMLoError = loError.Process( oErr )
	Throw loError

Finally
	loError = Thisform.oError
	loError.Cremark = ''
	loError.Ctracelogin = ''
	loError = Null

EndTry

Return This.oColDefaultParameters


ENDPROC
PROCEDURE ocolevents_access

Local loError As ErrorHandler Of "Tools\ErrorHandler\Prg\ErrorHandler.prg"

Try
	With This
		If Vartype( .oColEvents ) # 'O'
			.oColEvents = Newobject( 'PrxCollection', "Fw\TierAdapter\Comun\PrxBaseLibrary.prg" )

		Endif && Vartype( .oColEvents ) # 'O'

	Endwith

Catch To oErr
	loError = Thisform.oError
	Thisform.cXMLoError = loError.Process( oErr )
	Throw loError

Finally
	loError = Thisform.oError
	loError.Cremark = ''
	loError.Ctracelogin = ''
	loError = Null

Endtry
Return This.oColEvents


ENDPROC
PROCEDURE preparecode
Lparameters tcCode As String

#INCLUDE "FW\Comunes\Include\Praxis.h"

Local lcCode As String
Local loError As ErrorHandler Of "Tools\ErrorHandler\Prg\ErrorHandler.prg"

Try
	lcCode = 'LParameters toThis As Object, toSender As Object, toParam As Object'
	If ! Empty( tcCode )
		tcCode = Strtran( tcCode, 'ThisForm', 'This', 1, -1, 1 )
		lcCode = lcCode + CR + Strtran( tcCode, 'This', 'toThis', 1, -1, 1 )

	Else
		lcCode = lcCode + CR + 'Wait Window "Sin codigo" NoWait'

	Endif && ! Empty( tcCode )

Catch To oErr
	loError = Thisform.oError
	Thisform.cXMLoError = loError.Process( oErr )
	Throw loError

Finally
	loError = Thisform.oError
	loError.Cremark = ''
	loError.Ctracelogin = ''
	loError = Null

Endtry

Return lcCode

ENDPROC
PROCEDURE setclasslib
Lparameters tcClassLib As String

If ! Empty( tcClassLib )
	tcClassLib = Lower( tcClassLib )

	If ! ( tcClassLib $ Lower( Set( 'Classlib' ) ) ) And File( tcClassLib )
		TEXT To lcCommand NoShow TextMerge Pretext 15
			Set Classlib To '<<tcClassLib>>' Additive
		ENDTEXT

		&lcCommand

	Endif

Endif

ENDPROC
[END METHODS]
[START RESERVED1]
Class[END RESERVED1]
[START RESERVED2]
1[END RESERVED2]
[START RESERVED3]
ocolevents
ocoldefaultparameters
*ocolevents_access 
*ocoldefaultparameters_access 
*addcode 
*addeventcode 
*addorgetcontrol 
*addorgetevent 
*clearbindevents 
*filldefaultparameters 
*getpath 
*getref 
*getrefr 
*handlerevent 
*preparecode 
*addobjectto 
*applyproperty 
*applypropertiesto 
*loadconfig 
*loadcontrols 
*loadmethods 
*loadproperties 
*cloneobj 
*setclasslib 
[END RESERVED3]
[START RESERVED6]
Pixels[END RESERVED6]

[ RECORD]
[PLATFORM] COMMENT 
[UNIQUEID] RESERVED  
[OBJNAME] abmdynamicform
[START PROPERTIES]
Arial, 0, 9, 5, 15, 12, 32, 3, 0
Arial, 1, 9, 6, 15, 12, 32, 3, 0
[END PROPERTIES]
[START RESERVED1]
  ..\include\praxis.hî×È\; ..\include\foxpro.höØYj; ..\include\strings.hþÙÑeX9 ..\..\tieradapter\include\ta.hÛ.Kî:[END RESERVED1]
[EOF]
