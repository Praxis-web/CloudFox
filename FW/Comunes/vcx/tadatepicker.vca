SCCTEXT Version 4.0.0.2
PLATFORM C(8,0),UNIQUEID C(10,0),TIMESTAMP N(10,0),CLASS M(4,0),CLASSLOC M(4,0),BASECLASS M(4,0),OBJNAME M(4,0),PARENT M(4,0),PROPERTIES M(4,0),PROTECTED M(4,0),METHODS M(4,0),OBJCODE M(4,0),OLE M(4,0),OLE2 M(4,0),RESERVED1 M(4,0),RESERVED2 M(4,0),RESERVED3 M(4,0),RESERVED4 M(4,0),RESERVED5 M(4,0),RESERVED6 M(4,0),RESERVED7 M(4,0),RESERVED8 M(4,0),USER M(4,0)
1252

[ RECORD]
[PLATFORM] COMMENT 
[UNIQUEID] Class     
[START RESERVED1]
VERSION =   3.00[END RESERVED1]

[ RECORD]
[PLATFORM] WINDOWS 
[UNIQUEID] _0C60OH0UM
[CLASS] form
[BASECLASS] form
[OBJNAME] caldatemonth
[START PROPERTIES]
BorderStyle = 2
Top = 0
Left = 0
Height = 134
Width = 148
Desktop = .T.
ShowWindow = 2
DoCreate = .T.
Tag = "W=172,H=166"
ShowTips = .T.
Caption = ""
ControlBox = .F.
Closable = .F.
FontSize = 8
MaxButton = .F.
MinButton = .F.
Movable = .F.
Visible = .F.
TitleBar = 0
AlwaysOnTop = .T.
oparent = .NULL.
Name = "caldatemonth"
[END PROPERTIES]
[START METHODS]
PROCEDURE Deactivate
WITH THISFORM
	IF NOT ISNULL( .oParent )
		.oParent.SETFOCUS()	&& Go back to the parent object
		.oParent = .NULL.
	ENDIF
	.VISIBLE = .F.			&& Hide the calendar
ENDWITH

ENDPROC
PROCEDURE Init
This.Height = This.taCALENDAR.Height
This.Width = This.tacALENDAR.Width
ENDPROC
PROCEDURE Show
LPARAMETERS nStyle

THIS.taCalendar.Setfocus()	&& Make sure we're on the calendar object

ENDPROC
[END METHODS]
[START RESERVED1]
Class[END RESERVED1]
[START RESERVED2]
2[END RESERVED2]
[START RESERVED3]
oparent Parent object
[END RESERVED3]
[START RESERVED6]
Pixels[END RESERVED6]

[ RECORD]
[PLATFORM] WINDOWS 
[UNIQUEID] _0L115QS04
[CLASS] tacalendar
[CLASSLOC] tacal.vcx
[BASECLASS] container
[OBJNAME] taCalendar
[PARENT] caldatemonth
[START PROPERTIES]
Top = 0
Left = 0
BorderWidth = 0
Name = "taCalendar"
txtControl.Name = "txtControl"
cntHeader.cmdLastMonth.Name = "cmdLastMonth"
cntHeader.cmdNextMonth.Name = "cmdNextMonth"
cntHeader.lblMonthYear.Name = "lblMonthYear"
cntHeader.Name = "cntHeader"
[END PROPERTIES]
[START METHODS]
PROCEDURE canceldate
WITH THIS.PARENT
	.VISIBLE = .F.		&& Hide the calendar
	.oParent.Setfocus()	&& Go back to the parent object
ENDWITH

ENDPROC
PROCEDURE selectdate
LPARAMETERS lddateselected

WITH THIS.PARENT
	.oParent.dDate = lddateselected	&& Load selected date
	.VISIBLE = .F.					&& Hide the calendar
	.oParent.SETFOCUS()				&& Go back to parent object
ENDWITH

ENDPROC
[END METHODS]

[ RECORD]
[PLATFORM] COMMENT 
[UNIQUEID] RESERVED  
[OBJNAME] caldatemonth
[START PROPERTIES]
Arial, 0, 8, 5, 14, 11, 29, 3, 0
Arial, 0, 9, 5, 15, 12, 32, 3, 0
[END PROPERTIES]

[ RECORD]
[PLATFORM] WINDOWS 
[UNIQUEID] _0C60Q19TL
[CLASS] container
[BASECLASS] container
[OBJNAME] keycalmonthform
[START PROPERTIES]
Width = 100
Height = 24
BackStyle = 0
BorderWidth = 0
ocalendar = .NULL.
nhostformwindowtype = 0
value = (Ctod(""))
calbackcolor = 
calcurrentdaybackcolor = 
caldayandheaderforecolor = 
caldefaultwindowcolor = 
calshortday = 
caltodaybackcolor = 
caltodayforecolor = 
calweekdayheaderforecolor = 
cbackcolor = 
century = 1
cfontbold = 
cfontitalic = 
cfontname = 
cfontsize = 
cforecolor = 
controlsource = 
dateformat = 11
datepartinitfocus = 2
ddate = {}
lallowblankdate = .T.
lcalendarinstantiated = .F.
lexitontabonly = .F.
ltoplevelmenu = .F.
lupdowndisabled = .F.
readonly = .F.
Name = "keycalmonthform"
[END PROPERTIES]
[START METHODS]
PROCEDURE Destroy
WITH THIS
	IF NOT ISNULL( .oCalendar )
		.oCalendar.RELEASE()	&& Shut down the calendar
		.oCalendar = NULL
	ENDIF
ENDWITH

ENDPROC
PROCEDURE Init
Lparameters lnWidth, lnHeight	&& Optional Width and Height
Local cValue

Try

	This.Autofit()
	
*!*		THIS.InstantiateCalendar()
	
	
	If ( Vartype( lnWidth ) = 'N' And Not Empty( lnWidth ) )
		This.Width = Int( lnWidth )
	Endif

	If ( Vartype( lnHeight ) = 'N' And Not Empty( lnHeight ) )
		This.Height = Int( lnHeight )
	Endif

	With This
		.lCalendarInstantiated = Iif( Vartype( .oCalendar ) = 'O', .T., .F. )
		.dDate = .KeyDate.Value

		.KeyDate.lAllowBlankDate = .lAllowBlankDate
		.KeyDate.ControlSource = .ControlSource

		If Not Empty( .cBackColor )
			cValue = .cBackColor
			.KeyDate.BackColor = Rgb(&cValue)
		Endif
		.cBackColor = .KeyDate.BackColor

		If Not Empty( .cForeColor )
			cValue = .cForeColor
			.KeyDate.ForeColor = Rgb(&cValue)
		Endif
		.cForeColor = .KeyDate.ForeColor

		If ( Not Empty( .cFontBold ) And Vartype( .cFontBold ) = 'L' )
			.KeyDate.FontBold = .cFontBold
		Endif
		.cFontBold = .KeyDate.FontBold

		If ( Not Empty( .cFontItalic ) And Vartype( .cFontItalic ) = 'L' )
			.KeyDate.FontItalic = .cFontItalic
		Endif
		.cFontItalic = .KeyDate.FontItalic

		If ( Not Empty( .cFontName ) And Vartype( .cFontName ) = 'C' )
			.KeyDate.FontName = .cFontName
		Endif
		.cFontName = .KeyDate.FontName

		If ( Not Empty( .cFontSize ) And Vartype( .cFontSize ) = 'N' )
			.KeyDate.FontSize = .cFontSize
		Endif
		.cFontSize = .KeyDate.FontSize

		If Vartype( .ReadOnly ) = 'L'
			.KeyDate.ReadOnly = .ReadOnly
		Else
			.ReadOnly = .F.
		Endif

		If Not Empty( .ToolTipText )
			.KeyDate.ToolTipText = .ToolTipText
			.cmdKeyDate.ToolTipText = .ToolTipText
		Endif

		If Not Empty( .StatusBarText )
			.KeyDate.StatusBarText = .StatusBarText
			.cmdKeyDate.StatusBarText = .StatusBarText
		Endif

		If ( Not Empty( .DatePartInitFocus ) And Vartype( .DatePartInitFocus ) = 'N' )
			.DatePartInitFocus = Int( .DatePartInitFocus )
			If Inlist( .DatePartInitFocus, 1, 2, 3 )	&& Month, Day, Year
				.KeyDate.DatePartInitFocus = .DatePartInitFocus
			Else
				.DatePartInitFocus = 1	&& Default
			Endif
		Endif
		.DatePartInitFocus = .KeyDate.DatePartInitFocus

		If Vartype( .DateFormat ) = 'N'
			With .KeyDate
				Do Case		&& Date Format
					Case Inlist( This.DateFormat, 1, 9, 10 )		&& AMERICAN, USA, MDY
						.DateFormat = This.DateFormat
						.DateType = 1

					Case Inlist( This.DateFormat, 3, 4, 5, 6, 11 )	&& BRITISH, FRENCH, GERMAN, ITALIAN, DMY
						.DateFormat = This.DateFormat
						.DateType = 2

					Case Inlist( This.DateFormat, 2, 7, 8, 12 )		&& ANSI, JAPAN, TAIWAN, YMD
						.DateFormat = This.DateFormat
						.DateType = 3

					Otherwise	&& Invalid format, default to 10-MDY
						.DateFormat = 10
						.DateType = 1
						This.DateFormat = 10
				Endcase
			Endwith
		Else
			.DateFormat = 10		&& Default to MDY
			.KeyDate.DateFormat = 10
		Endif

		If ( Vartype( .Century ) = 'N' And Inlist( .Century, 0, 1, 2 ) )
			.KeyDate.Century = .Century
		Else
			.Century = 1	&& Default
		Endif

		If Vartype( .lUpDownDisabled ) = 'L'
			.KeyDate.lUpDownDisabled = .lUpDownDisabled
		Else
			.lUpDownDisabled = .KeyDate.lUpDownDisabled	&& Default (.F.)
		Endif

		If Vartype( .lExitOnTabOnly ) = 'L'
			.KeyDate.lExitOnTabOnly = .lExitOnTabOnly
		Else
			.lExitOnTabOnly = .KeyDate.lExitOnTabOnly	&& Default (.T.)
		Endif

		*	Set up the popup Calendar properties and synchronize the host class properties
*!*			With .oCalendar.taCalendar
*!*				.CurrentDayBackcolor = This.CalCurrentDayBackcolor
*!*				.DayAndHeaderForecolor = This.CalDayAndHeaderForecolor
*!*				.DefaultWindowColor = This.CalDefaultWindowcolor
*!*				.ShortDay = This.CalShortDay
*!*				.TodayForecolor = This.CalTodayForecolor
*!*				.WeekdayHeaderForecolor = This.CalWeekdayHeaderForecolor
*!*			Endwith
	Endwith

Catch To oErr
	Local loError As ErrorHandler Of "Tools\ErrorHandler\Prg\ErrorHandler.prg"
	loError = Newobject( "ErrorHandler", "Tools\ErrorHandler\Prg\ErrorHandler.prg" )
	loError.Process( oErr )

Finally

Endtry


ENDPROC
PROCEDURE Resize
*!*	WITH THIS
*!*		.cmdKeyDate.LEFT = .WIDTH - .cmdKeyDate.WIDTH - 2
*!*		.cmdKeyDate.HEIGHT = .HEIGHT - 4
*!*		.KeyDate.WIDTH = .WIDTH - .cmdKeyDate.WIDTH - 4
*!*		.KeyDate.HEIGHT = .HEIGHT - 4
*!*	ENDWITH

ENDPROC
PROCEDURE autofit
This.Keydate.Top = 2
This.Keydate.Left = 2
This.cmdKeyDate.Height = This.Keydate.Height
This.cmdKeyDate.Left = This.Keydate.Width + 2
This.Width = This.Keydate.Width + This.cmdKeyDate.Width + 4
This.Height = This.Keydate.Height + 4
ENDPROC
PROCEDURE backcolor_assign
LPARAMETERS vNewVal
*To do: Modify this routine for the Assign method
THIS.BackColor = m.vNewVal
This.Keydate.BackColor = m.vNewVal

ENDPROC
PROCEDURE calbackcolor_access
RETURN THIS.oCalendar.taCalendar.Backcolor

ENDPROC
PROCEDURE calbackcolor_assign
LPARAMETERS vNewVal

WITH THIS.oCalendar.taCalendar
	IF ( VARTYPE( m.vNewVal ) = 'N' AND m.vNewVal = -1 )	&& Reset to default
		.ResetToDefault( "Backcolor" )
	ELSE
		.Backcolor = m.vNewVal
		THIS.CalBackcolor = .Backcolor	&& Synchonize properties
	ENDIF
ENDWITH

ENDPROC
PROCEDURE calcurrentdaybackcolor_access
RETURN THIS.oCalendar.taCalendar.CurrentDayBackcolor

ENDPROC
PROCEDURE calcurrentdaybackcolor_assign
LPARAMETERS vNewVal

WITH THIS.oCalendar.taCalendar
	.CurrentDayBackcolor = m.vNewVal
	THIS.CalCurrentDayBackcolor = .CurrentDayBackcolor	&& Synchonize properties
ENDWITH

ENDPROC
PROCEDURE caldayandheaderforecolor_access
RETURN THIS.oCalendar.taCalendar.DayAndHeaderForecolor

ENDPROC
PROCEDURE caldayandheaderforecolor_assign
LPARAMETERS vNewVal

WITH THIS.oCalendar.taCalendar
	.DayAndHeaderForecolor = m.vNewVal
	THIS.CalDayAndHeaderForecolor = .DayAndHeaderForecolor	&& Synchronize properties
ENDWITH

ENDPROC
PROCEDURE caldefaultwindowcolor_assign
LPARAMETERS vNewVal

WITH THIS.oCalendar.taCalendar
	.DefaultWindowColor = m.vNewVal
	THIS.CalDefaultWindowColor = .DefaultWindowColor	&& Synchronize properties
ENDWITH

ENDPROC
PROCEDURE calshortday_assign
LPARAMETERS vNewVal

WITH THIS.oCalendar.taCalendar
	.ShortDay = m.vNewVal
	THIS.CalShortDay = .ShortDay	&& Synchronize properties
ENDWITH

ENDPROC
PROCEDURE caltodaybackcolor_access
RETURN THIS.oCalendar.taCalendar.TodayBackcolor

ENDPROC
PROCEDURE caltodaybackcolor_assign
LPARAMETERS vNewVal

WITH THIS.oCalendar.taCalendar
	.TodayBackcolor = m.vNewVal
	THIS.CalTodayBackcolor = .TodayBackcolor	&& Synchronize properties
ENDWITH

ENDPROC
PROCEDURE caltodayforecolor_access
RETURN THIS.oCalendar.taCalendar.TodayForecolor

ENDPROC
PROCEDURE caltodayforecolor_assign
LPARAMETERS vNewVal

WITH THIS.oCalendar.taCalendar
	.TodayForecolor = m.vNewVal
	THIS.CalTodayForecolor = .TodayForecolor	&& Synchronize properties
ENDWITH

ENDPROC
PROCEDURE calweekdayheaderforecolor_access
RETURN THIS.oCalendar.taCalendar.WeekdayHeaderForecolor

ENDPROC
PROCEDURE calweekdayheaderforecolor_assign
LPARAMETERS vNewVal

WITH THIS.oCalendar.taCalendar
	.WeekdayHeaderForecolor = m.vNewVal
	THIS.CalWeekdayHeaderForecolor = .WeekdayHeaderForecolor	&& Synchronize properties
ENDWITH

ENDPROC
PROCEDURE cbackcolor_assign
LPARAMETERS vNewVal

DO CASE
	CASE VARTYPE( vNewVal ) = 'N'
		vNewVal = INT( vNewVal )
		THIS.KeyDate.BackColor = vNewVal
		THIS.cBackColor = ALLTRIM( PADL( MOD( vNewVal, 256 ), 3 ) ) + ',' + ;
							ALLTRIM( PADL( MOD( INT( vNewVal / 256 ), 256 ), 3 ) ) + ',' + ;
							ALLTRIM( PADL( INT( vNewVal / 65536 ), 3 ) )

	CASE VARTYPE( vNewVal ) = 'C'
		THIS.KeyDate.BackColor = RGB(&vNewVal)
		THIS.cBackColor = vNewVal
ENDCASE

ENDPROC
PROCEDURE century_assign
LPARAMETERS vNewVal

IF ( VARTYPE( m.vNewVal ) = 'N' AND INLIST( m.vNewVal, 0, 1, 2 ) )
	WITH THIS
		.Century = m.vNewVal
		.KeyDate.Century = m.vNewVal
	ENDWITH
ENDIF

ENDPROC
PROCEDURE cfontbold_assign
LPARAMETERS vNewVal

IF VARTYPE( vNewVal ) = 'L'
	THIS.KeyDate.FontBold = vNewVal
	THIS.cFontBold = vNewVal
ENDIF

ENDPROC
PROCEDURE cfontitalic_assign
LPARAMETERS vNewVal

IF VARTYPE( vNewVal ) = 'L'
	THIS.KeyDate.FontItalic = vNewVal
	THIS.cFontItalic = vNewVal
ENDIF

ENDPROC
PROCEDURE cfontname_assign
LPARAMETERS vNewVal

IF ( VARTYPE( vNewVal ) = 'C' AND NOT EMPTY( vNewVal ) )
	THIS.KeyDate.FontName = vNewVal
	THIS.cFontName = vNewVal
ENDIF

ENDPROC
PROCEDURE cfontsize_assign
LPARAMETERS vNewVal

IF ( VARTYPE( vNewVal ) = 'N' AND NOT EMPTY( vNewVal ) )
	THIS.KeyDate.FontSize = vNewVal
	THIS.cFontSize = vNewVal
ENDIF

ENDPROC
PROCEDURE cforecolor_assign
LPARAMETERS vNewVal

DO CASE
	CASE VARTYPE( vNewVal ) = 'N'
		vNewVal = INT( vNewVal )
		THIS.KeyDate.ForeColor = vNewVal
		THIS.cForeColor = ALLTRIM( PADL( MOD( vNewVal, 256 ), 3 ) ) + ',' + ;
							ALLTRIM( PADL( MOD( INT( vNewVal / 256 ), 256 ), 3 ) ) + ',' + ;
							ALLTRIM( PADL( INT( vNewVal / 65536 ), 3 ) )

	CASE VARTYPE( vNewVal ) = 'C'
		THIS.KeyDate.ForeColor = RGB(&vNewVal)
		THIS.cForeColor = vNewVal
ENDCASE

ENDPROC
PROCEDURE controlsource_assign
LPARAMETERS vNewVal

IF VARTYPE( vNewVal ) = 'C'
	WITH THIS
		.KeyDate.ControlSource = vNewVal
		.ControlSource = vNewVal
	ENDWITH
ENDIF

ENDPROC
PROCEDURE dateformat_assign
LPARAMETERS vNewVal

IF VARTYPE( m.vNewVal ) != 'N'
	RETURN
ENDIF

m.vNewVal = INT( m.vNewVal )

IF NOT BETWEEN( m.vNewVal, 0, 12 )	&& No Short/Long Dates because we can't
	RETURN							&& determine the specific date type
ENDIF

WITH THIS
	IF m.vNewVal = 0						&& Default SET DATE
		DO CASE
			CASE SET( "DATE" ) $ "AMERICANUSAMDY"
				WITH .KeyDate
					.DateFormat = 10
					.DateType = 1
				ENDWITH
				.DateFormat = 10

			CASE SET( "DATE" ) $ "BRITISHFRENCHGERMANITALIANDMY"
				WITH .KeyDate
					.DateFormat = 11
					.DateType = 2
				ENDWITH
				.DateFormat = 11

			CASE SET( "DATE" ) $ "ANSIJAPANTAIWANYMD"
				WITH .KeyDate
					.DateFormat = 12
					.DateType = 3
				ENDWITH
				.DateFormat = 11

			CASE SET( "DATE" ) $ "SHORTLONG"
				RETURN
		ENDCASE
	ELSE
		DO CASE
			CASE INLIST( m.vNewVal, 1, 9, 10 )		&& AMERICAN, USA, MDY
				.KeyDate.DateType = 1

			CASE INLIST( m.vNewVal, 3, 4, 5, 6, 11 )	&& BRITISH, FRENCH, GERMAN, ITALIAN, DMY
				.KeyDate.DateType = 2


			CASE INLIST( m.vNewVal, 2, 7, 8, 12 )	&& ANSI, JAPAN, TAIWAN, YMD
				.KeyDate.DateType = 3
		ENDCASE

		.KeyDate.DateFormat = m.vNewVal
		.DateFormat = m.vNewVal
	ENDIF
ENDWITH

ENDPROC
PROCEDURE datepartinitfocus_assign
LPARAMETERS vNewVal

IF INLIST( m.vNewVal, 1, 2, 3 )	&& Month, Day, Year
	THIS.KeyDate.DatePartInitFocus = m.vNewVal
	THIS.DatePartInitFocus = m.vNewVal
ENDIF

ENDPROC
PROCEDURE ddate_access
*	Make sure the control date is synchronized with
*	The underlying textbox value
THIS.dDate = THIS.KeyDate.Value
RETURN THIS.dDate

ENDPROC
PROCEDURE ddate_assign
LPARAMETERS vNewVal

IF VARTYPE( m.vNewVal ) = 'D'	&& We only do Date data types
	WITH THIS
		IF NOT EMPTY( m.vNewVal ) OR .lAllowBlankDate
			.KeyDate.Value = m.vNewVal
			IF .lCalendarInstantiated AND NOT EMPTY( m.vNewVal )
				.oCalendar.taCalendar.Date = m.vNewVal
			ENDIF
			THIS.dDate = m.vNewVal
		ENDIF
	ENDWITH
ENDIF

ENDPROC
PROCEDURE enabled_assign
LPARAMETERS vNewVal

IF VARTYPE( vNewVal ) != 'L'
	RETURN
ENDIF

WITH THIS
	.Enabled = m.vNewVal
	.KeyDate.Enabled = m.vNewVal
	.cmdKeyDate.Enabled = m.vNewVal
ENDWITH

ENDPROC
PROCEDURE instantiatecalendar
#INCLUDE "FW\Comunes\Include\Praxis.h"

With This

	If Not .lCalendarInstantiated
		Do Case
			Case Thisform.ShowWindow = 2	&& Host form is Top Level
				.oCalendar = Newobject( "CalDateMonth", Addbs(FL_LIBS)+"taDatePicker" )

			Case Thisform.ShowWindow = 1	&& Host form is In Top Level
				.oCalendar = Newobject( "CalDateMonthInTopLevelForm", Addbs(FL_LIBS)+"taDatePicker" )

			Otherwise						&& Host form in In Screen
				*!*	RR, 15/01/2004 - Quito el control de Modal / Modeless ya que en forms Modales, el control
				*!*	no se cerraba si se hacía click fuera del mismo y el usuario estaba obligado a seleccionar
				*!*	una fecha - Ver también This.InstantiateCalendar
				If Thisform.WindowType = 0	&& Host form is Modeless
					.oCalendar = Newobject( "CalDateMonth", Addbs(FL_LIBS)+"taDatePicker" )

				Else
					.oCalendar = Newobject( "CalDateMonthModal", Addbs(FL_LIBS)+"taDatePicker" )

				Endif
		Endcase

		If ( Type( "THIS.oCalendar" ) = 'O' And Not Isnull( .oCalendar ) )
			.oCalendar.Visible = .F.
		Else
			.oCalendar = Null			&& Can't let 'em use it
			.cmdKeyDate.Enabled = .F.	&& if it didn't instantiate
		Endif

		.lCalendarInstantiated = Iif( Vartype( .oCalendar ) = 'O', .T., .F. )

		If .lCalendarInstantiated
			*	Set up the popup Calendar properties and synchronize the host class properties
			With .oCalendar.taCalendar
				.CurrentDayBackcolor = This.CalCurrentDayBackcolor
				.DayAndHeaderForecolor = This.CalDayAndHeaderForecolor
				.DefaultWindowColor = This.CalDefaultWindowcolor
				.ShortDay = This.CalShortDay
				.TodayForecolor = This.CalTodayForecolor
				.WeekdayHeaderForecolor = This.CalWeekdayHeaderForecolor
			Endwith
		Endif

	Endif
Endwith


ENDPROC
PROCEDURE lallowblankdate_assign
LPARAMETERS vNewVal

IF VARTYPE( m.vNewVal ) = 'L'
	THIS.lAllowBlankdate = vNewVal
	THIS.KeyDate.lAllowBlankDate = vNewVal
ENDIF

ENDPROC
PROCEDURE lcalendarinstantiated_assign
LPARAMETERS vNewVal

THIS.lCalendarInstantiated = VARTYPE( THIS.oCalendar ) = 'O'	&& Makes it read only

ENDPROC
PROCEDURE lexitontabonly_assign
LPARAMETERS vNewVal

WITH THIS
	IF VARTYPE( m.vNewVal ) = 'L'
		.lExitOnTabOnly = m.vNewVal
		.KeyDate.lExitOnTabOnly = m.vNewVal
	ELSE
		.lExitOnTabOnly = .KeyDate.lExitOnTabOnly
	ENDIF
ENDWITH

ENDPROC
PROCEDURE ltoplevelmenu_assign
LPARAMETERS vNewVal
*To do: Modify this routine for the Assign method
THIS.ltoplevelmenu = m.vNewVal

ENDPROC
PROCEDURE lupdowndisabled_assign
LPARAMETERS vNewVal

IF VARTYPE( m.vNewVal ) = 'L'
	THIS.lUpDownDisabled = m.vNewVal
	THIS.KeyDate.lUpDownDisabled = m.vNewVal
ENDIF

ENDPROC
PROCEDURE programmaticchange
*	ProgrammaticChange Event Hook
LPARAMETERS kdValue

ENDPROC
PROCEDURE readonly_assign
LPARAMETERS vNewVal

IF VARTYPE( vNewVal ) != 'L'
	RETURN
ENDIF

WITH THIS
	.ReadOnly = m.vNewVal
	.KeyDate.ReadOnly = m.vNewVal
	.cmdKeyDate.Enabled = NOT m.vNewVal
ENDWITH

ENDPROC
PROCEDURE refreshfocus
THIS.KeyDate.GotFocus()

ENDPROC
PROCEDURE setposition
If This.lCalendarInstantiated
	With This.oCalendar
*!*	RR, 15/01/2004 - Quito el control de Modal / Modeless ya que en forms Modales, el control
*!*	no se cerraba si se hacía click fuera del mismo y el usuario estaba obligado a seleccionar
*!*	una fecha - Ver también This.InstantiateCalendar
		If Thisform.WindowType = 0		&& Host form is Modeless
			.Top = Objtoclient( This, 1 ) + Sysmetric(9) + This.Height + ;
				Iif( Thisform.BorderStyle = 3, Sysmetric(4), Sysmetric(13) ) + ;
				Iif( Thisform.ShowWindow = 2, Thisform.Top, Objtoclient( Thisform, 1 ) ) + ;
				Iif( This.lTopLevelMenu, Sysmetric(20), 0 )
			.Left = Objtoclient( This, 2 ) + Iif( Thisform.BorderStyle = 3, Sysmetric(3), Sysmetric(12) ) + ;
				Iif( Thisform.ShowWindow = 2, Thisform.Left, Objtoclient( Thisform, 2 ) )

			If ( ( .Top + .Height ) > Sysmetric(2) )						&& Adjust to "drop up" if
				.Top = .Top - .Height - This.Height - ( 2 * Sysmetric(13) )	&& near bottom of screen
			Endif
		Else							&& Host form is Modal
			.Top = Objtoclient( This, 1 ) + This.Height + Sysmetric(9) + ;
				IIF( Thisform.BorderStyle = 3, Sysmetric(4), Sysmetric(13) ) + ;
				THISFORM.Top + Iif( This.lTopLevelMenu, Sysmetric(20), 0 )
			.Left = Objtoclient( This, 2 ) + Iif( Thisform.BorderStyle = 3, Sysmetric(3), Sysmetric(12) ) + ;
				THISFORM.Left

			If ( ( .Top + .Height ) > Iif( .Desktop, Sysmetric(2), _vfp.Height ) )	&& Adjust to "drop up" if
				.Top = .Top - .Height - This.Height - ( 2 * Sysmetric(13) )	&& near bottom of screen
			Endif
		Endif

		Do Case		&& Shift horizontal position if close to right edge
			Case .ShowWindow = 0	&& In Screen
				If .Left + .Width > _vfp.Width
					.Left = .Left - .Width + This.Width - 2 * Sysmetric(3)
				Endif

			Case .ShowWindow = 1	&& In Top-Level Form
				If .Left + .Width > Iif( .Desktop, Sysmetric(1), Thisform.Width )
					.Left = .Left - .Width + This.Width - 2 * Sysmetric(3)
				Endif

			Case .ShowWindow = 2	&& Top-Level Form
				If .Left + .Width > Sysmetric(1)
					.Left = .Left - .Width + This.Width - 2 * Sysmetric(3)
				Endif
		Endcase

	Endwith

Endif

ENDPROC
PROCEDURE statusbartext_assign
LPARAMETERS vNewVal

IF VARTYPE( m.vNewVal ) = 'C'
	WITH THIS
		.StatusBarText = m.vNewVal
		.KeyDate.StatusBarText = m.vNewVal
		.cmdKeyDate.StatusBarText = m.vNewVal
	ENDWITH
ENDIF

ENDPROC
PROCEDURE tooltiptext_assign
LPARAMETERS vNewVal

IF VARTYPE( m.vNewVal ) = 'C'
	WITH THIS
		.ToolTipText = m.vNewVal
		.KeyDate.ToolTipText = m.vNewVal
		.cmdKeyDate.ToolTipText = m.vNewVal
	ENDWITH
ENDIF

ENDPROC
PROCEDURE value_access
Return This.KeyDate.Value
ENDPROC
PROCEDURE value_assign
Lparameters vNewVal

If Vartype( vNewVal ) == "T"
	vNewVal = Ttod( vNewVal )
EndIf

This.value = m.vNewVal
This.KeyDate.Value = m.vNewVal
ENDPROC
[END METHODS]
[START RESERVED1]
Class[END RESERVED1]
[START RESERVED2]
3[END RESERVED2]
[START RESERVED3]
ocalendar Object reference to the ActiveX calendar form
nhostformwindowtype Host form Modeless/Modal indicator
value Specifies the current state of a control.
calbackcolor
calcurrentdaybackcolor
caldayandheaderforecolor
caldefaultwindowcolor
calshortday
caltodaybackcolor
caltodayforecolor
calweekdayheaderforecolor
cbackcolor
century Specifies if the century is displayed as four or two digits.
cfontbold
cfontitalic
cfontname
cfontsize
cforecolor KeyDate ForeColor
controlsource Control Source for the date text box control
dateformat DateFormat of the underlying KeyDate contrrol
datepartinitfocus Date part highlighted when control receives focus: 1- Month (default), 2-Day, 3-Year
ddate Control's current date
lallowblankdate Allow blank ( empty ) dates
lcalendarinstantiated Status of the drop-down calendar instantiation ( Boolean )
lexitontabonly Exit the control on (Shift+)Tab only
ltoplevelmenu Indicates whether the control's parent form is Top-Level with a menu
lupdowndisabled Flag indicating whether Up / Down arrow functionality is disabled ( +/- functionality only )
readonly Read only for underlying controls
*instantiatecalendar Instantiates the ActiveX MonthView
*setposition Sets the ActiveX calendar form position
*calbackcolor_access 
*calbackcolor_assign 
*calcurrentdaybackcolor_access 
*calcurrentdaybackcolor_assign 
*caldayandheaderforecolor_access 
*caldayandheaderforecolor_assign 
*caldefaultwindowcolor_assign 
*calshortday_assign 
*caltodaybackcolor_access 
*caltodaybackcolor_assign 
*caltodayforecolor_access 
*caltodayforecolor_assign 
*calweekdayheaderforecolor_access 
*calweekdayheaderforecolor_assign 
*cbackcolor_assign 
*century_assign 
*cfontbold_assign 
*cfontitalic_assign 
*cfontname_assign 
*cfontsize_assign 
*cforecolor_assign 
*controlsource_assign 
*dateformat_assign 
*datepartinitfocus_assign 
*ddate_access 
*ddate_assign 
*lallowblankdate_assign 
*lcalendarinstantiated_assign 
*lexitontabonly_assign 
*ltoplevelmenu_assign 
*lupdowndisabled_assign 
*readonly_assign 
*keypress Hook event for the underlying KeyDate KeyPress event
*programmaticchange Hook for ProgrammaticChange of underlying KeyDate control
*refreshfocus Fires the underlying KeyDate control GotFocus() for refreshing the selected date part
*when Hook event for the underlying KeyDate When event
*enabled_assign 
*statusbartext_assign 
*tooltiptext_assign 
*value_access 
*value_assign 
*autofit Auto-resizes one or all visible grid columns.
*backcolor_assign 
[END RESERVED3]
[START RESERVED6]
Pixels[END RESERVED6]

[ RECORD]
[PLATFORM] WINDOWS 
[UNIQUEID] _0C80J94DM
[CLASS] keydate
[CLASSLOC] tadatepicker.vcx
[BASECLASS] textbox
[OBJNAME] Keydate
[PARENT] keycalmonthform
[START PROPERTIES]
Height = 24
Left = 0
TabIndex = 1
Top = 0
Width = 80
Name = "Keydate"
[END PROPERTIES]
[START METHODS]
PROCEDURE KeyPress
Lparameters nkeycode,nshiftaltctrl


*!*	TEXT To lcMsg NoShow TextMerge Pretext 03
*!*	nKeyCode = <<nKeyCode>> - nshiftaltctrl = <<nshiftaltctrl>>
*!*	ENDTEXT

*!*	Wait Window Nowait Noclear lcMsg

*!*	IF ( nKeyCode = -3 OR ( nKeyCode = 160 AND nShiftAltCtrl = 4 ) )	&& F4, Alt+DownArrow
*!*
*!*		NODEFAULT
*!*		IF .T. && THIS.PARENT.lCalendarInstantiated
*!*			WITH THIS.PARENT.cmdKeyDate
*!*				.SETFOCUS()
*!*				.CLICK()
*!*			ENDWITH
*!*		ENDIF
*!*	ELSE
*!*		DODEFAULT( nKeyCode, nShiftAltCtrl )
*!*	ENDIF

Do Case
	Case ( nkeycode = -3 Or ( nkeycode = 160 And nshiftaltctrl = 4 ) )	&& F4, Alt+DownArrow
		Nodefault
		With This.Parent.cmdKeyDate
			.SetFocus()
			.Click()
		Endwith

	Case ( nkeycode = 93 And nshiftaltctrl = 1 )   && Shift+F10 / Menu
		Nodefault
		This.MouseDown( 2, 0, 0, 13, .T. )

	Otherwise
		DoDefault( nkeycode, nshiftaltctrl )

Endcase

ENDPROC
[END METHODS]

[ RECORD]
[PLATFORM] WINDOWS 
[UNIQUEID] _0C80TJLH0
[CLASS] commandbutton
[BASECLASS] commandbutton
[OBJNAME] cmdKeyDate
[PARENT] keycalmonthform
[START PROPERTIES]
Top = 0
Left = 82
Height = 24
Width = 16
Picture = ..\image\bmp\dropdown.bmp
Caption = ""
TabStop = .F.
Name = "cmdKeyDate"
[END PROPERTIES]
[START METHODS]
PROCEDURE Click
LOCAL ldDate

WITH THIS.PARENT
	.InstantiateCalendar()
	IF ( .lCalendarInstantiated AND NOT .KeyDate.ReadOnly )
		.SetPosition()	&& Position the popup calendar

		ldDate = IIF( EMPTY( .KeyDate.VALUE ), DATE(), .KeyDate.VALUE )

		WITH .oCalendar
			.oParent = THIS.PARENT	&& Topmost container ( Control )
			.taCalendar.Date = ldDate		&& Load the current date
			.SHOW()
		ENDWITH
	ENDIF
ENDWITH

ENDPROC
[END METHODS]

[ RECORD]
[PLATFORM] COMMENT 
[UNIQUEID] RESERVED  
[OBJNAME] keycalmonthform
[START PROPERTIES]
Arial, 0, 9, 5, 15, 12, 32, 3, 0
[END PROPERTIES]
[START RESERVED1]
  ..\include\praxis.h¦•s~\V ..\include\foxpro.h®–ÊrûF ..\include\strings.h¶—ZŠOJ* ..\..\..\tools\namespaces\include\system.h¾˜±ÙF[END RESERVED1]

[ RECORD]
[PLATFORM] WINDOWS 
[UNIQUEID] _0C60QTEJU
[CLASS] control
[BASECLASS] control
[OBJNAME] tadatepicker
[START PROPERTIES]
Width = 100
Height = 27
BackStyle = 0
BorderWidth = 0
ToolTipText = ""
StatusBarText = ""
ddate = {}
controlsource = 
cbackcolor = 
cforecolor = 
cfontname = 
cfontsize = 
cfontbold = 
cfontitalic = 
dateformat = 11
datepartinitfocus = 2
century = 1
lexitontabonly = .T.
calcurrentdaybackcolor = -1
caldayandheaderforecolor = -1
caldefaultwindowcolor = .T.
caltodaybackcolor = -1
caltodayforecolor = -1
calweekdayheaderforecolor = -1
calbackcolor = -1
_memberdata =       81<VFPData><memberdata name="autofit" type="method" display="Autofit"/></VFPData>

inputmask = 99-99-9999
Name = "tadatepicker"
[END PROPERTIES]
[START METHODS]
PROCEDURE Init
Lparameters lnWidth, lnHeight	&& Optional Width and Height
Local cValue

Try

	
	This.KeyCalMonthForm.keydate.AutoFit()
	This.Autofit()
	
	If ( Vartype( lnWidth ) = 'N' And Not Empty( lnWidth ) )
		This.Width = Int( lnWidth )
	Endif

	If ( Vartype( lnHeight ) = 'N' And Not Empty( lnHeight ) )
		This.Height = Int( lnHeight )
	Endif

	With This
		.lCalendarInstantiated = Iif( Vartype( .KeyCalMonthForm.oCalendar ) = 'O', .T., .F. )
		.dDate = .KeyCalMonthForm.KeyDate.Value

		.KeyCalMonthForm.KeyDate.lAllowBlankDate = .lAllowBlankDate
		.KeyCalMonthForm.KeyDate.ControlSource = .ControlSource

		If Not Empty( .cBackColor )
			cValue = .cBackColor
			.KeyCalMonthForm.KeyDate.BackColor = Rgb(&cValue)
		Endif
		.cBackColor = .KeyCalMonthForm.KeyDate.BackColor

		If Not Empty( .cForeColor )
			cValue = .cForeColor
			.KeyCalMonthForm.KeyDate.ForeColor = Rgb(&cValue)
		Endif
		.cForeColor = .KeyCalMonthForm.KeyDate.ForeColor

		If ( Not Empty( .cFontBold ) And Vartype( .cFontBold ) = 'L' )
			.KeyCalMonthForm.KeyDate.FontBold = .cFontBold
		Endif
		.cFontBold = .KeyCalMonthForm.KeyDate.FontBold

		If ( Not Empty( .cFontItalic ) And Vartype( .cFontItalic ) = 'L' )
			.KeyCalMonthForm.KeyDate.FontItalic = .cFontItalic
		Endif
		.cFontItalic = .KeyCalMonthForm.KeyDate.FontItalic

		If ( Not Empty( .cFontName ) And Vartype( .cFontName ) = 'C' )
			.KeyCalMonthForm.KeyDate.FontName = .cFontName
		Endif
		.cFontName = .KeyCalMonthForm.KeyDate.FontName

		If ( Not Empty( .cFontSize ) And Vartype( .cFontSize ) = 'N' )
			.KeyCalMonthForm.KeyDate.FontSize = .cFontSize
		Endif
		.cFontSize = .KeyCalMonthForm.KeyDate.FontSize

		If Vartype( .ReadOnly ) = 'L'
			.KeyCalMonthForm.KeyDate.ReadOnly = .ReadOnly
		Else
			.ReadOnly = .F.
		Endif

		If Not Empty( .ToolTipText )
			.KeyCalMonthForm.KeyDate.ToolTipText = .ToolTipText
			.KeyCalMonthForm.cmdKeyDate.ToolTipText = .ToolTipText
		Endif

		If Not Empty( .StatusBarText )
			.KeyCalMonthForm.KeyDate.StatusBarText = .StatusBarText
			.KeyCalMonthForm.cmdKeyDate.StatusBarText = .StatusBarText
		Endif

		If ( Not Empty( .DatePartInitFocus ) And Vartype( .DatePartInitFocus ) = 'N' )
			.DatePartInitFocus = Int( .DatePartInitFocus )
			If Inlist( .DatePartInitFocus, 1, 2, 3 )	&& Month, Day, Year
				.KeyCalMonthForm.KeyDate.DatePartInitFocus = .DatePartInitFocus
			Else
				.DatePartInitFocus = 1	&& Default
			Endif
		Endif
		.DatePartInitFocus = .KeyCalMonthForm.KeyDate.DatePartInitFocus

		If Vartype( .DateFormat ) = 'N'
			With .KeyCalMonthForm.KeyDate
				Do Case		&& Date Format
					Case Inlist( This.DateFormat, 1, 9, 10 )		&& AMERICAN, USA, MDY
						.DateFormat = This.DateFormat
						.DateType = 1

					Case Inlist( This.DateFormat, 3, 4, 5, 6, 11 )	&& BRITISH, FRENCH, GERMAN, ITALIAN, DMY
						.DateFormat = This.DateFormat
						.DateType = 2

					Case Inlist( This.DateFormat, 2, 7, 8, 12 )		&& ANSI, JAPAN, TAIWAN, YMD
						.DateFormat = This.DateFormat
						.DateType = 3

					Otherwise	&& Invalid format, default to 10-MDY
						.DateFormat = 10
						.DateType = 1
						This.DateFormat = 10
				Endcase
			Endwith
		Else
			.DateFormat = 10		&& Default to MDY
			.KeyCalMonthForm.KeyDate.DateFormat = 10
		Endif

		If ( Vartype( .Century ) = 'N' And Inlist( .Century, 0, 1, 2 ) )
			.KeyCalMonthForm.KeyDate.Century = .Century
		Else
			.Century = 1	&& Default
		Endif

		If Vartype( .lUpDownDisabled ) = 'L'
			.KeyCalMonthForm.KeyDate.lUpDownDisabled = .lUpDownDisabled
		Else
			.lUpDownDisabled = .KeyCalMonthForm.KeyDate.lUpDownDisabled	&& Default (.F.)
		Endif

		If Vartype( .lExitOnTabOnly ) = 'L'
			.KeyCalMonthForm.KeyDate.lExitOnTabOnly = .lExitOnTabOnly
		Else
			.lExitOnTabOnly = .KeyCalMonthForm.KeyDate.lExitOnTabOnly	&& Default (.T.)
		Endif

		*	Set up the popup Calendar properties and synchronize the host class properties
		With .KeyCalMonthForm.oCalendar.taCalendar
			.CurrentDayBackcolor = This.CalCurrentDayBackcolor
			.DayAndHeaderForecolor = This.CalDayAndHeaderForecolor
			.DefaultWindowColor = This.CalDefaultWindowcolor
			.ShortDay = This.CalShortDay
			.TodayForecolor = This.CalTodayForecolor
			.WeekdayHeaderForecolor = This.CalWeekdayHeaderForecolor
		Endwith
	Endwith

Catch To oErr
	Local loError As ErrorHandler Of "Tools\ErrorHandler\Prg\ErrorHandler.prg"
	loError = Newobject( "ErrorHandler", "Tools\ErrorHandler\Prg\ErrorHandler.prg" )
	loError.Process( oErr )

Finally

Endtry


ENDPROC
PROCEDURE Resize
WITH THIS.KeyCalMonthForm
	.Width = THIS.Width
	.Height = THIS.Height
ENDWITH

ENDPROC
PROCEDURE autofit
WITH THIS.KeyCalMonthForm
	This.Width 	= .KeyDate.WIDTH + .cmdKeyDate.WIDTH + 4
	This.Height = .KeyDate.HEIGHT + 4
EndWith

ENDPROC
PROCEDURE calbackcolor_access
RETURN THIS.KeyCalMonthForm.oCalendar.taCalendar.Backcolor

ENDPROC
PROCEDURE calbackcolor_assign
LPARAMETERS vNewVal

WITH THIS.KeyCalMonthForm.oCalendar.taCalendar
	IF ( VARTYPE( m.vNewVal ) = 'N' AND m.vNewVal = -1 )	&& Reset to default
		.ResetToDefault( "Backcolor" )
	ELSE
		.Backcolor = m.vNewVal
		THIS.CalBackcolor = .Backcolor	&& Synchonize properties
	ENDIF
ENDWITH

ENDPROC
PROCEDURE calcurrentdaybackcolor_access
RETURN THIS.KeyCalMonthForm.oCalendar.taCalendar.CurrentDayBackcolor

ENDPROC
PROCEDURE calcurrentdaybackcolor_assign
LPARAMETERS vNewVal

WITH THIS.KeyCalMonthForm.oCalendar.taCalendar
	.CurrentDayBackcolor = m.vNewVal
	THIS.CalCurrentDayBackcolor = .CurrentDayBackcolor	&& Synchonize properties
ENDWITH

ENDPROC
PROCEDURE caldayandheaderforecolor_access
RETURN THIS.KeyCalMonthForm.oCalendar.taCalendar.DayAndHeaderForecolor

ENDPROC
PROCEDURE caldayandheaderforecolor_assign
LPARAMETERS vNewVal

WITH THIS.KeyCalMonthForm.oCalendar.taCalendar
	.DayAndHeaderForecolor = m.vNewVal
	THIS.CalDayAndHeaderForecolor = .DayAndHeaderForecolor	&& Synchronize properties
ENDWITH

ENDPROC
PROCEDURE caldefaultwindowcolor_assign
LPARAMETERS vNewVal

WITH THIS.KeyCalMonthForm.oCalendar.taCalendar
	.DefaultWindowColor = m.vNewVal
	THIS.CalDefaultWindowColor = .DefaultWindowColor	&& Synchronize properties
ENDWITH

ENDPROC
PROCEDURE calshortday_assign
LPARAMETERS vNewVal

WITH THIS.KeyCalMonthForm.oCalendar.taCalendar
	.ShortDay = m.vNewVal
	THIS.CalShortDay = .ShortDay	&& Synchronize properties
ENDWITH

ENDPROC
PROCEDURE caltodaybackcolor_access
RETURN THIS.KeyCalMonthForm.oCalendar.taCalendar.TodayBackcolor

ENDPROC
PROCEDURE caltodaybackcolor_assign
LPARAMETERS vNewVal

WITH THIS.KeyCalMonthForm.oCalendar.taCalendar
	.TodayBackcolor = m.vNewVal
	THIS.CalTodayBackcolor = .TodayBackcolor	&& Synchronize properties
ENDWITH

ENDPROC
PROCEDURE caltodayforecolor_access
RETURN THIS.KeyCalMonthForm.oCalendar.taCalendar.TodayForecolor

ENDPROC
PROCEDURE caltodayforecolor_assign
LPARAMETERS vNewVal

WITH THIS.KeyCalMonthForm.oCalendar.taCalendar
	.TodayForecolor = m.vNewVal
	THIS.CalTodayForecolor = .TodayForecolor	&& Synchronize properties
ENDWITH

ENDPROC
PROCEDURE calweekdayheaderforecolor_access
RETURN THIS.KeyCalMonthForm.oCalendar.taCalendar.WeekdayHeaderForecolor

ENDPROC
PROCEDURE calweekdayheaderforecolor_assign
LPARAMETERS vNewVal

WITH THIS.KeyCalMonthForm.oCalendar.taCalendar
	.WeekdayHeaderForecolor = m.vNewVal
	THIS.CalWeekdayHeaderForecolor = .WeekdayHeaderForecolor	&& Synchronize properties
ENDWITH

ENDPROC
PROCEDURE cbackcolor_assign
LPARAMETERS vNewVal

DO CASE
	CASE VARTYPE( vNewVal ) = 'N'
		vNewVal = INT( vNewVal )
		THIS.KeyCalMonthForm.KeyDate.BackColor = vNewVal
		THIS.cBackColor = ALLTRIM( PADL( MOD( vNewVal, 256 ), 3 ) ) + ',' + ;
							ALLTRIM( PADL( MOD( INT( vNewVal / 256 ), 256 ), 3 ) ) + ',' + ;
							ALLTRIM( PADL( INT( vNewVal / 65536 ), 3 ) )

	CASE VARTYPE( vNewVal ) = 'C'
		THIS.KeyCalMonthForm.KeyDate.BackColor = RGB(&vNewVal)
		THIS.cBackColor = vNewVal
ENDCASE

ENDPROC
PROCEDURE century_assign
LPARAMETERS vNewVal

IF ( VARTYPE( m.vNewVal ) = 'N' AND INLIST( m.vNewVal, 0, 1, 2 ) )
	WITH THIS
		.Century = m.vNewVal
		.KeyCalMonthForm.KeyDate.Century = m.vNewVal
	ENDWITH
ENDIF

ENDPROC
PROCEDURE cfontbold_assign
LPARAMETERS vNewVal

IF VARTYPE( vNewVal ) = 'L'
	THIS.KeyCalMonthForm.KeyDate.FontBold = vNewVal
	THIS.cFontBold = vNewVal
ENDIF

ENDPROC
PROCEDURE cfontitalic_assign
LPARAMETERS vNewVal

IF VARTYPE( vNewVal ) = 'L'
	THIS.KeyCalMonthForm.KeyDate.FontItalic = vNewVal
	THIS.cFontItalic = vNewVal
ENDIF

ENDPROC
PROCEDURE cfontname_assign
LPARAMETERS vNewVal

IF ( VARTYPE( vNewVal ) = 'C' AND NOT EMPTY( vNewVal ) )
	THIS.KeyCalMonthForm.KeyDate.FontName = vNewVal
	THIS.cFontName = vNewVal
ENDIF

ENDPROC
PROCEDURE cfontsize_assign
LPARAMETERS vNewVal

IF ( VARTYPE( vNewVal ) = 'N' AND NOT EMPTY( vNewVal ) )
	THIS.KeyCalMonthForm.KeyDate.FontSize = vNewVal
	THIS.cFontSize = vNewVal
ENDIF

ENDPROC
PROCEDURE cforecolor_assign
LPARAMETERS vNewVal

DO CASE
	CASE VARTYPE( vNewVal ) = 'N'
		vNewVal = INT( vNewVal )
		THIS.KeyCalMonthForm.KeyDate.ForeColor = vNewVal
		THIS.cForeColor = ALLTRIM( PADL( MOD( vNewVal, 256 ), 3 ) ) + ',' + ;
							ALLTRIM( PADL( MOD( INT( vNewVal / 256 ), 256 ), 3 ) ) + ',' + ;
							ALLTRIM( PADL( INT( vNewVal / 65536 ), 3 ) )

	CASE VARTYPE( vNewVal ) = 'C'
		THIS.KeyCalMonthForm.KeyDate.ForeColor = RGB(&vNewVal)
		THIS.cForeColor = vNewVal
ENDCASE

ENDPROC
PROCEDURE controlsource_assign
LPARAMETERS vNewVal

IF VARTYPE( vNewVal ) = 'C'
	WITH THIS
		.KeyCalMonthForm.KeyDate.ControlSource = vNewVal
		.ControlSource = vNewVal
	ENDWITH
ENDIF

ENDPROC
PROCEDURE dateformat_assign
LPARAMETERS vNewVal

IF VARTYPE( m.vNewVal ) != 'N'
	RETURN
ENDIF

m.vNewVal = INT( m.vNewVal )

IF NOT BETWEEN( m.vNewVal, 0, 12 )	&& No Short/Long Dates because we can't
	RETURN							&& determine the specific date type
ENDIF

WITH THIS
	IF m.vNewVal = 0						&& Default SET DATE
		DO CASE
			CASE SET( "DATE" ) $ "AMERICANUSAMDY"
				WITH .KeyCalMonthForm.KeyDate
					.DateFormat = 10
					.DateType = 1
				ENDWITH
				.DateFormat = 10

			CASE SET( "DATE" ) $ "BRITISHFRENCHGERMANITALIANDMY"
				WITH .KeyCalMonthForm.KeyDate
					.DateFormat = 11
					.DateType = 2
				ENDWITH
				.DateFormat = 11

			CASE SET( "DATE" ) $ "ANSIJAPANTAIWANYMD"
				WITH .KeyCalMonthForm.KeyDate
					.DateFormat = 12
					.DateType = 3
				ENDWITH
				.DateFormat = 11

			CASE SET( "DATE" ) $ "SHORTLONG"
				RETURN
		ENDCASE
	ELSE
		DO CASE
			CASE INLIST( m.vNewVal, 1, 9, 10 )		&& AMERICAN, USA, MDY
				.KeyCalMonthForm.KeyDate.DateType = 1

			CASE INLIST( m.vNewVal, 3, 4, 5, 6, 11 )	&& BRITISH, FRENCH, GERMAN, ITALIAN, DMY
				.KeyCalMonthForm.KeyDate.DateType = 2


			CASE INLIST( m.vNewVal, 2, 7, 8, 12 )	&& ANSI, JAPAN, TAIWAN, YMD
				.KeyCalMonthForm.KeyDate.DateType = 3
		ENDCASE

		.KeyCalMonthForm.KeyDate.DateFormat = m.vNewVal
		.DateFormat = m.vNewVal
	ENDIF
ENDWITH

ENDPROC
PROCEDURE datepartinitfocus_assign
LPARAMETERS vNewVal

IF INLIST( m.vNewVal, 1, 2, 3 )	&& Month, Day, Year
	THIS.KeyCalMonthForm.KeyDate.DatePartInitFocus = m.vNewVal
	THIS.DatePartInitFocus = m.vNewVal
ENDIF

ENDPROC
PROCEDURE ddate_access
*	Make sure the control date is synchronized with
*	The underlying textbox value
THIS.dDate = THIS.KeyCalMonthForm.KeyDate.Value
RETURN THIS.dDate

ENDPROC
PROCEDURE ddate_assign
LPARAMETERS vNewVal

IF VARTYPE( m.vNewVal ) = 'D'	&& We only do Date data types
	WITH THIS
		IF NOT EMPTY( m.vNewVal ) OR .lAllowBlankDate
			.KeyCalMonthForm.KeyDate.Value = m.vNewVal
			IF .lCalendarInstantiated AND NOT EMPTY( m.vNewVal )
				.KeyCalMonthForm.oCalendar.taCalendar.Date = m.vNewVal
			ENDIF
			THIS.dDate = m.vNewVal
		ENDIF
	ENDWITH
ENDIF

ENDPROC
PROCEDURE enabled_assign
LPARAMETERS vNewVal

IF VARTYPE( vNewVal ) != 'L'
	RETURN
ENDIF

WITH THIS
	.Enabled = m.vNewVal
	.KeyCalMonthForm.KeyDate.Enabled = m.vNewVal
	.KeyCalMonthForm.cmdKeyDate.Enabled = m.vNewVal
ENDWITH

ENDPROC
PROCEDURE interactivechange
*	InteractiveChange Event Hook
LPARAMETERS kdValue

ENDPROC
PROCEDURE keypress
*	KeyPress Event Hook
LPARAMETERS nKeyCode, nShiftAltCtrl

ENDPROC
PROCEDURE lallowblankdate_assign
LPARAMETERS vNewVal

IF VARTYPE( m.vNewVal ) = 'L'
	THIS.lAllowBlankdate = vNewVal
	THIS.KeyCalMonthForm.KeyDate.lAllowBlankDate = vNewVal
ENDIF

ENDPROC
PROCEDURE lcalendarinstantiated_assign
LPARAMETERS vNewVal

THIS.lCalendarInstantiated = VARTYPE( THIS.KeyCalMonthForm.oCalendar ) = 'O'	&& Makes it read only

ENDPROC
PROCEDURE lexitontabonly_assign
LPARAMETERS vNewVal

WITH THIS
	IF VARTYPE( m.vNewVal ) = 'L'
		.lExitOnTabOnly = m.vNewVal
		.KeyCalMonthForm.KeyDate.lExitOnTabOnly = m.vNewVal
	ELSE
		.lExitOnTabOnly = .KeyCalMonthForm.KeyDate.lExitOnTabOnly
	ENDIF
ENDWITH

ENDPROC
PROCEDURE lupdowndisabled_assign
LPARAMETERS vNewVal

IF VARTYPE( m.vNewVal ) = 'L'
	THIS.lUpDownDisabled = m.vNewVal
	THIS.KeyCalMonthForm.KeyDate.lUpDownDisabled = m.vNewVal
ENDIF

ENDPROC
PROCEDURE programmaticchange
*	ProgrammaticChange Event Hook
LPARAMETERS kdValue

ENDPROC
PROCEDURE readonly_assign
LPARAMETERS vNewVal

IF VARTYPE( vNewVal ) != 'L'
	RETURN
ENDIF

WITH THIS
	.ReadOnly = m.vNewVal
	.KeyCalMonthForm.KeyDate.ReadOnly = m.vNewVal
	.KeyCalMonthForm.cmdKeyDate.Enabled = NOT m.vNewVal
ENDWITH

ENDPROC
PROCEDURE refreshfocus
THIS.KeyCalMonthForm.KeyDate.GotFocus()

ENDPROC
PROCEDURE statusbartext_assign
LPARAMETERS vNewVal

IF VARTYPE( m.vNewVal ) = 'C'
	WITH THIS
		.StatusBarText = m.vNewVal
		.KeyCalMonthForm.KeyDate.StatusBarText = m.vNewVal
		.KeyCalMonthForm.cmdKeyDate.StatusBarText = m.vNewVal
	ENDWITH
ENDIF

ENDPROC
PROCEDURE tooltiptext_assign
LPARAMETERS vNewVal

IF VARTYPE( m.vNewVal ) = 'C'
	WITH THIS
		.ToolTipText = m.vNewVal
		.KeyCalMonthForm.KeyDate.ToolTipText = m.vNewVal
		.KeyCalMonthForm.cmdKeyDate.ToolTipText = m.vNewVal
	ENDWITH
ENDIF

ENDPROC
PROCEDURE value_access
Return This.KeyCalMonthForm.KeyDate.Value
ENDPROC
PROCEDURE value_assign
Lparameters vNewVal

This.value = m.vNewVal
This.KeyCalMonthForm.KeyDate.Value = m.vNewVal
ENDPROC
PROCEDURE when
*	When Event Hook

ENDPROC
[END METHODS]
[START RESERVED1]
Class[END RESERVED1]
[START RESERVED2]
2[END RESERVED2]
[START RESERVED3]
ddate Control's current date
lallowblankdate Allow blank ( empty ) dates
lcalendarinstantiated Status of the drop-down calendar instantiation ( Boolean )
ltoplevelmenu Indicates whether the control's parent form is Top-Level with a menu
controlsource Control Source for the date text box control
cbackcolor KeyDate backcolor
cforecolor KeyDate ForeColor
cfontname KeyDate Font name
cfontsize KeyDate Font size
cfontbold KeyDate Font bold
cfontitalic KeyDate Font Italic
dateformat DateFormat of the underlying KeyDate contrrol
datepartinitfocus Date part highlighted when control receives focus: 1- Month (default), 2-Day, 3-Year
readonly Read only for underlying controls
century Sets the Century property of the underlying KeyDate control
lupdowndisabled Flag indicating whether Up / Down arrow functionality is disabled ( +/- functionality only )
lexitontabonly Exit the control on (Shift+)Tab only
calcurrentdaybackcolor Current day back color for the popup calendar
caldayandheaderforecolor Day and header Month and Year forecolor for the popup calendar
caldefaultwindowcolor Default window color flag for the popup calendar
calshortday Short weekday name flag for the popup calendar
caltodaybackcolor Today backcolor for the popup calendar
caltodayforecolor Today forecolor for the popup calendar
calweekdayheaderforecolor Weekday header names forecolor
calbackcolor Background color for the popup Calendar
value Specifies the current state of a control.
_memberdata XML Metadata for customizable properties
inputmask Specifies how data is entered and displayed in a control.
*ddate_access 
*ddate_assign 
*lallowblankdate_assign 
*lcalendarinstantiated_assign 
*controlsource_assign 
*cbackcolor_assign 
*cforecolor_assign 
*cfontname_assign 
*cfontsize_assign 
*cfontbold_assign 
*cfontitalic_assign 
*tooltiptext_assign 
*statusbartext_assign 
*dateformat_assign 
*interactivechange Hook for InteractiveChange of underlying KeyDate control
*programmaticchange Hook for ProgrammaticChange of underlying KeyDate control
*datepartinitfocus_assign 
*keypress Hook event for the underlying KeyDate KeyPress event
*when Hook event for the underlying KeyDate When event
*readonly_assign 
*refreshfocus Fires the underlying KeyDate control GotFocus() for refreshing the selected date part
*century_assign 
*lupdowndisabled_assign 
*lexitontabonly_assign 
*calcurrentdaybackcolor_assign 
*caldayandheaderforecolor_assign 
*caldefaultwindowcolor_assign 
*calshortday_assign 
*caltodaybackcolor_assign Today backcolor for the popup calendar
*caltodayforecolor_assign 
*calweekdayheaderforecolor_assign 
*calbackcolor_assign 
*calbackcolor_access 
*calcurrentdaybackcolor_access 
*caldayandheaderforecolor_access 
*caltodaybackcolor_access 
*caltodayforecolor_access 
*calweekdayheaderforecolor_access 
*enabled_assign 
*value_access 
*value_assign 
*autofit Auto-resizes one or all visible grid columns.
[END RESERVED3]
[START RESERVED6]
Pixels[END RESERVED6]

[ RECORD]
[PLATFORM] WINDOWS 
[UNIQUEID] _0C80JD70L
[CLASS] keycalmonthform
[CLASSLOC] tadatepicker.vcx
[BASECLASS] container
[OBJNAME] KeyCalMonthForm
[PARENT] tadatepicker
[START PROPERTIES]
Top = 0
Left = 0
Width = 100
Name = "KeyCalMonthForm"
keydate.Name = "keydate"
cmdKeyDate.Name = "cmdKeyDate"
[END PROPERTIES]
[START METHODS]
PROCEDURE keydate.GotFocus
*	GotFocus Event Hook
DODEFAULT()
THIS.PARENT.PARENT.GotFocus()

ENDPROC
PROCEDURE keydate.InteractiveChange
*	InteractiveChange Event Hook
DODEFAULT()
THIS.PARENT.PARENT.InteractiveChange( THIS.Value )

ENDPROC
PROCEDURE keydate.KeyPress
*	KeyPress Event Hook
LPARAMETERS nkeycode,nshiftaltctrl

DODEFAULT( nkeycode, nshiftaltctrl )
THIS.PARENT.PARENT.KeyPress( nkeycode, nshiftaltctrl )

ENDPROC
PROCEDURE keydate.LostFocus
*	LostFocus Event Hook
DODEFAULT()
THIS.PARENT.PARENT.LostFocus()

ENDPROC
PROCEDURE keydate.ProgrammaticChange
*	ProgrammaticChange Event Hook
THIS.PARENT.PARENT.ProgrammaticChange( THIS.Value )

ENDPROC
PROCEDURE keydate.When
*	When Event Hook
DODEFAULT()
THIS.PARENT.PARENT.When()

ENDPROC
[END METHODS]

[ RECORD]
[PLATFORM] COMMENT 
[UNIQUEID] RESERVED  
[OBJNAME] tadatepicker
[START PROPERTIES]
Arial, 0, 9, 5, 15, 12, 32, 3, 0
[END PROPERTIES]

[ RECORD]
[PLATFORM] WINDOWS 
[UNIQUEID] _0CY0P18WF
[CLASS] caldatemonth
[CLASSLOC] tadatepicker.vcx
[BASECLASS] form
[OBJNAME] caldatemonthmodal
[START PROPERTIES]
Desktop = .F.
ShowWindow = 0
DoCreate = .T.
Name = "caldatemonthmodal"
tacalendar.txtControl.Name = "txtControl"
tacalendar.cntHeader.cmdLastMonth.Name = "cmdLastMonth"
tacalendar.cntHeader.cmdNextMonth.Name = "cmdNextMonth"
tacalendar.cntHeader.lblMonthYear.Name = "lblMonthYear"
tacalendar.cntHeader.Name = "cntHeader"
tacalendar.Name = "tacalendar"
[END PROPERTIES]
[START RESERVED1]
Class[END RESERVED1]
[START RESERVED2]
1[END RESERVED2]
[START RESERVED6]
Pixels[END RESERVED6]

[ RECORD]
[PLATFORM] COMMENT 
[UNIQUEID] RESERVED  
[OBJNAME] caldatemonthmodal
[START PROPERTIES]
Arial, 0, 8, 5, 14, 11, 30, 3, 0
Arial, 0, 9, 5, 15, 12, 32, 3, 0
[END PROPERTIES]

[ RECORD]
[PLATFORM] WINDOWS 
[UNIQUEID] _0CY0Z48LL
[CLASS] caldatemonth
[CLASSLOC] tadatepicker.vcx
[BASECLASS] form
[OBJNAME] caldatemonthintoplevelform
[START PROPERTIES]
Desktop = .T.
ShowWindow = 1
DoCreate = .T.
Name = "caldatemonthintoplevelform"
tacalendar.txtControl.Name = "txtControl"
tacalendar.cntHeader.cmdLastMonth.Name = "cmdLastMonth"
tacalendar.cntHeader.cmdNextMonth.Name = "cmdNextMonth"
tacalendar.cntHeader.lblMonthYear.Name = "lblMonthYear"
tacalendar.cntHeader.Name = "cntHeader"
tacalendar.Name = "tacalendar"
[END PROPERTIES]
[START RESERVED1]
Class[END RESERVED1]
[START RESERVED2]
1[END RESERVED2]
[START RESERVED6]
Pixels[END RESERVED6]

[ RECORD]
[PLATFORM] COMMENT 
[UNIQUEID] RESERVED  
[OBJNAME] caldatemonthintoplevelform
[START PROPERTIES]
Arial, 0, 8, 5, 14, 11, 30, 3, 0
Arial, 0, 9, 5, 15, 12, 32, 3, 0
[END PROPERTIES]

[ RECORD]
[PLATFORM] WINDOWS 
[UNIQUEID] _RH20ODKH7
[CLASS] textbox
[BASECLASS] textbox
[OBJNAME] keydate
[START PROPERTIES]
DateFormat = 11
Alignment = 3
Value = (Date())
Margin = 0
StatusBarText = "Ingrese la fecha"
ToolTipText = "Fecha"
Width = 113
currentdate = 
lallowblankdate = .T.
datetype = 1
datepartinitfocus = 1
yearsellength = 4
daystart = 0
dayend = 1
monthstart = 3
monthend = 4
yearstart = 6
yearend = 9
lexitontabonly = .T.
Name = "keydate"
[END PROPERTIES]
[START PROTECTED]
currentdate
daystart
dayend
monthstart
monthend
yearstart
yearend
setyearsellength
setstartendpositions
[END PROTECTED]
[START METHODS]
PROCEDURE Click
THIS.SetFocus()
ENDPROC
PROCEDURE Error
LPARAMETERS nError, cMethod, nLine

*	We assume that errors in this control are
*	due to user entering in invalid date then
*	using arrow keys. Since too many tests on
*	date validity might slow the control down
*	so we opt to ignore the error. If desired
*	this can be overidden as necessary
*	The following code can be uncommented for
*	simple debugging
*	MESSAGEBOX ( "Error: " + ALLTRIM( STR( nError ) ) + CHR(13) + CHR(10) + ;
*					"Method: " + cMethod )
RETURN
ENDPROC
PROCEDURE GotFocus
LOCAL lnStart, lnLength

WITH THIS
	.SetYearSelLength()		&& Set these property values each time we enter
	.SetStartEndPositions()	&& the control in case of programmatic changes
	.CurrentDate = .VALUE

	DO CASE
		CASE .DatePartInitFocus = 2	&& Day
			lnLength = 2
			lnStart = .DayStart

		CASE .DatePartInitFocus = 3	&& Year
			lnLength = .YearSelLength
			lnStart = .YearStart

		OTHERWISE					&& Month
			lnLength = 2
			lnStart = .MonthStart
	ENDCASE

	.SelStart = lnStart
	.SelLength = lnLength
ENDWITH

ENDPROC
PROCEDURE Init
Try
	With This
		.SetYearSelLength()
		.SetStartEndPositions()
		.FieldName = Substr( .Name, 4 )
		.AutoFit()
		.Value = {}
	Endwith

Catch To oErr
	oErr.Message = oErr.Message + "(Instanciando control "+This.Name+")"
	Throw oErr

Finally

Endtry



ENDPROC
PROCEDURE InteractiveChange
WITH THIS
	IF INLIST( .DateType, 1, 2 )	&& MDY, DMY
		DO CASE
			CASE .SelStart = 3
				.SelLength = 2

			CASE .SelStart = 6
				.SelLength = .YearSelLength
		ENDCASE
	ELSE							&& YMD
		IF INLIST( .SelStart, .MonthStart, .DayStart )	&& 5, 8
			.SelLength = 2
		ENDIF
	ENDIF
ENDWITH

ENDPROC
PROCEDURE KeyPress
LPARAMETERS nKeyCode, nShiftAltCtrl

WITH THIS
	DO CASE
		CASE nKeyCode = 32					&& Space Bar ( DATE() )
			NODEFAULT
			IF .ReadOnly
				RETURN
			ENDIF
			.VALUE = DATE()
			.REFRESH
			.SETFOCUS()
		
		CASE nKeyCode = 1					&& Home ( Last Date entered )
			NODEFAULT
			IF .ReadOnly
				RETURN
			ENDIF
			.VALUE = .CurrentDate
			.REFRESH
			.SETFOCUS()
		
		CASE nKeyCode = 4		&& Right Arrow
			IF .ReadOnly
				NODEFAULT
				RETURN
			ENDIF
			IF .DateType = 3	&& YMD 0123/56/89, 01/34/67
				DO CASE
					CASE .SelStart < .MonthStart	&& 5
						NODEFAULT
						.SelStart = .MonthStart		&& Moving to second date part ( Month )
						.SelLength = 2

					CASE .SelStart > .YearEnd AND .SelStart < .DayStart	&& 3, 8
						NODEFAULT
						.SelStart = .DayStart		&& Moving to third date part ( Day )
						.SelLength = 2

					CASE .SelStart >= .DayStart
						NODEFAULT
						IF NOT .lExitOnTabOnly
							KEYBOARD '{TAB}'		&& Exiting the control
						ENDIF
					
					OTHERWISE
						NODEFAULT
				ENDCASE
			ELSE				&& MDY/DMY 01/34/6789, 01/34/67
				DO CASE			&& Month and Day SelLengths are the same so we use
					CASE .SelStart < 3	&& Literals for performance here
						NODEFAULT
						.SelStart = 3	&& Moving to second date part ( Day or Month )
						.SelLength = 2

					CASE .SelStart > 1 AND .SelStart < 6
						NODEFAULT
						.SelStart = 6	&& Moving to third date part ( Year )
						.SelLength = .YearSelLength
					
					CASE .SelStart >= .YearStart
						NODEFAULT
						IF NOT .lExitOnTabOnly
							KEYBOARD '{TAB}'		&& Exiting the control
						ENDIF

					OTHERWISE
						NODEFAULT
				ENDCASE
			ENDIF

		CASE nKeyCode = 19					&& Left Arrow
			IF .ReadOnly
				NODEFAULT
				RETURN
			ENDIF
			IF .DateType = 3	&& YMD 0123/56/89, 01/34/67
				DO CASE
					CASE .SelStart > .MonthEnd	&& 7
						NODEFAULT
						.SelStart = .MonthStart	&& Moving to second date part ( Month )
						.SelLength = 2

					CASE .SelStart > .YearEnd AND .SelStart < .DayStart	&& 3, 8
						NODEFAULT
						.SelStart = 0	&& Moving to first date part ( Year )
						.SelLength = .YearSelLength

					CASE .SelStart < .MonthStart
						NODEFAULT
						IF NOT .lExitOnTabOnly
							KEYBOARD '{BACKTAB}'	&& Exiting the control
						ENDIF
					
					OTHERWISE
						NODEFAULT
				ENDCASE
			ELSE				&& MDY/DMY 01/34/6789, 01/34/67
				DO CASE			&& Month and Day SelLengths are the same so we use
					CASE .SelStart > 5	&& Literals for performance here
						NODEFAULT
						.SelStart = 3	&& Moving to second date part ( Day or Month )
						.SelLength = 2

					CASE .SelStart > 1 AND .SelStart < 6
						NODEFAULT
						.SelStart = 0	&& Moving to first date part ( Month or Day )
						.SelLength = 2

					CASE .SelStart < 3
						NODEFAULT
						IF NOT .lExitOnTabOnly
							KEYBOARD '{BACKTAB}'	&& Exiting the control
						ENDIF
					
					OTHERWISE
						NODEFAULT
				ENDCASE
			ENDIF

		CASE INLIST( nKeyCode, 5, 43, 61 )	&& Up Arrow, '+'('=') ( Increment )
			NODEFAULT
			IF ( ( .lUpDownDisabled AND nKeyCode = 5 ) OR .ReadOnly )	&& Up Arrow disabled
				RETURN
			ENDIF

			DO CASE
				CASE .DateType = 1	&& MDY 01/34/6789, 01/34/67
					DO CASE			&& Use literals whenever possible for performance
						CASE .SelStart < 3						&& Month
							.VALUE = GOMONTH( .VALUE, 1 )
							.REFRESH
							.SelStart = 0
							.SelLength = 2

						CASE .SelStart > 1 AND .SelStart < 6	&& Day
							.VALUE = .VALUE + 1
							.REFRESH
							.SelStart = 3
							.SelLength = 2

						OTHERWISE								&& Year
							.VALUE = GOMONTH( .VALUE, 12 )
							.REFRESH
							.SelStart = 6
							.SelLength = .YearSelLength	&& 4
					ENDCASE

				CASE .DateType = 2	&& DMY 01/34/6789, 01/34/67
					DO CASE
						CASE .SelStart < 3						&& Day
							.VALUE = .VALUE + 1
							.REFRESH
							.SelStart = 0
							.SelLength = 2

						CASE .SelStart > 1 AND .SelStart < 6	&& Month
							.VALUE = GOMONTH( .VALUE, 1 )
							.REFRESH
							.SelStart = 3
							.SelLength = 2

						OTHERWISE								&& Year
							.VALUE = GOMONTH( .VALUE, 12 )
							.REFRESH
							.SelStart = 6
							.SelLength = .YearSelLength	&& 4
					ENDCASE

				OTHERWISE			&& YMD 0123/56/89, 01/34/67
					DO CASE
						CASE .SelStart < .MonthStart	&& 5	&& Year
							.VALUE = GOMONTH( .VALUE, 12 )
							.REFRESH
							.SelStart = 0
							.SelLength = .YearSelLength	&& 4

						CASE .SelStart > .YearEnd AND .SelStart < .DayStart	&& Month ( 3, 8 )
							.VALUE = GOMONTH( .VALUE, 1 )
							.REFRESH
							.SelStart = .MonthStart	&& 5
							.SelLength = 2

						OTHERWISE								&& Day
							.VALUE = .VALUE + 1
							.REFRESH
							.SelStart = .DayStart	&& 8
							.SelLength = 2
					ENDCASE
				ENDCASE

		CASE INLIST( nKeyCode, 24, 45 )	&& Down Arrow, '-' ( Decrement )
			NODEFAULT
			IF ( ( .lUpDownDisabled AND nKeyCode = 24 ) OR .ReadOnly )	&& Down Arrow disabled
				RETURN
			ENDIF

			DO CASE
				CASE .DateType = 1	&& MDY 01/34/6789, 01/34/67
					DO CASE			&& Use literals whenever possible for performance
						CASE .SelStart < 3						&& Month
							.VALUE = GOMONTH( .VALUE, -1 )
							.REFRESH
							.SelStart = 0
							.SelLength = 2

						CASE .SelStart > 1 AND .SelStart < 6	&& Day
							.VALUE = .VALUE - 1
							.REFRESH
							.SelStart = 3
							.SelLength = 2

						OTHERWISE								&& Year
							.VALUE = GOMONTH( .VALUE, -12 )
							.REFRESH
							.SelStart = 6
							.SelLength = .YearSelLength
					ENDCASE

				CASE .DateType = 2	&& DMY 01/34/6789, 01/34/67
					DO CASE
						CASE .SelStart < 3						&& Day
							.VALUE = .VALUE - 1
							.REFRESH
							.SelStart = 0
							.SelLength = 2

						CASE .SelStart > 1 AND .SelStart < 6	&& Month
							.VALUE = GOMONTH( .VALUE, -1 )
							.REFRESH
							.SelStart = 3
							.SelLength = 2

						OTHERWISE								&& Year
							.VALUE = GOMONTH( .VALUE, -12 )
							.REFRESH
							.SelStart = 6
							.SelLength = .YearSelLength
					ENDCASE

				OTHERWISE			&& YMD 0123/56/89, 01/34/67
					DO CASE
						CASE .SelStart < .MonthStart	&& 5	&& Year
							.VALUE = GOMONTH( .VALUE, -12 )
							.REFRESH
							.SelStart = 0
							.SelLength = .YearSelLength	&& 4

						CASE .SelStart > .YearEnd AND .SelStart < .DayStart	&& Month ( 3, 8 )
							.VALUE = GOMONTH( .VALUE, -1 )
							.REFRESH
							.SelStart = .MonthStart	&& 5
							.SelLength = 2

						OTHERWISE								&& Day
							.VALUE = .VALUE - 1
							.REFRESH
							.SelStart = .DayStart	&& 8
							.SelLength = 2
					ENDCASE
			ENDCASE

		CASE nKeyCode = 7								&& Del
			IF ( .ReadOnly OR NOT .lAllowBlankDate )
				NODEFAULT
				RETURN
			ENDIF
			.VALUE = {  /  /  }

		CASE ( nKeyCode > 47 AND nKeyCode < 60 )		&& Pass Digits
			IF .ReadOnly
				NODEFAULT
				RETURN
			ENDIF

		CASE INLIST( nKeyCode, 9, 15 )					&& Pass TAB, Shift+TAB

		CASE ( nKeyCode = 13 )							&& Pass the Enter key

		CASE ( nKeyCode = 127 )							&& Pass the Backspace key

		CASE ( nKeyCode = 27 )							&& Pass the Escape key

		OTHERWISE										&& Inhibit all other key strokes
			NODEFAULT
	ENDCASE
ENDWITH

ENDPROC
PROCEDURE MouseDown
Lparameters nButton, nShift, nXCoord, nYCoord, lManual

Do Case

	Case nButton = 2 And nShift = 0 && Right Click
		Local ldValue As Date
		Local lnKeyPress As Integer

		lnKeyPress = 0

*!*			Text To lcMsg NoShow TextMerge Pretext 03
*!*			<<Program()>>
*!*			nXCoord = <<nXCoord>>
*!*			nYCoord = <<nYCoord>>
*!*			
*!*			Mrow() = <<Mrow()>>
*!*			Mcol() = <<Mcol()>>		
*!*			EndText
*!*			
*!*			Wait WINDOW NOWAIT NOCLEAR lcMsg
		
		*!*	Definir el menu de la fecha.
		
		If lManual
			Define Popup fechas SHORTCUT Relative From nXCoord,nYCoord
			
		Else
			Define Popup fechas SHORTCUT Relative From Mrow(),Mcol()
			
		Endif

		
		Define Bar 1 Of fechas Prompt "Pri\<mer día del año"
		Define Bar 2 Of fechas Prompt "\<Primer día del mes"
		Define Bar 3 Of fechas Prompt "\<Primer día de la semana"
		Define Bar 4 Of fechas Prompt "Día de \<hoy"
		Define Bar 5 Of fechas Prompt "\<Ultimo día de la semana"
		Define Bar 6 Of fechas Prompt "\<Ultimo día del mes"
		Define Bar 7 Of fechas Prompt "U\<ltimo día del año"
		
		Define Bar 991 Of fechas Prompt "\-"

		Define Bar 21 Of fechas Prompt "Mes anterior"
		Define Bar 22 Of fechas Prompt "Mes siguiente"
		Define Bar 23 Of fechas Prompt "Año anterior"
		Define Bar 24 Of fechas Prompt "Año siguiente"
		

		On Selection Bar 1 Of fechas ldValue = Ctod( "01/01/" + Alltrim( Str( Year( Date() ) ) ) )
		On Selection Bar 2 Of fechas ldValue = ( Date() - Day( Date() ) + 1 )
		On Selection Bar 3 Of fechas ldValue = ( Date() - Dow( Date(), 2 ) + 1 )
		On Selection Bar 4 Of fechas ldValue = Date()
		On Selection Bar 5 Of fechas ldValue = ( Date() - Dow( Date(), 2 ) + 7 )
		On Selection Bar 6 Of fechas ldValue = Gomont( Date(), 1 ) - Day( Gomont( Date(), 1 ) )
		On Selection Bar 7 Of fechas ldValue = Ctod( "31/12/" + Alltrim( Str( Year( Date() ) ) ) )
		
		On Selection Bar 21 Of fechas lnKeyPress = 18
		On Selection Bar 22 Of fechas lnKeyPress = 3
		On Selection Bar 23 Of fechas lnKeyPress = 31
		On Selection Bar 24 Of fechas lnKeyPress = 30
		

		Activate Popup fechas

		If Not Empty( ldValue )
			This.Value = ldValue
		EndIf
		
		If !Empty( lnKeyPress )
			Do Case
				Case lnKeyPress = 18	&& PageUp ( Previous Month )
					This.Value = GOMONTH( This.Value, -1 )

				Case lnKeyPress = 3	&& PageDn ( Next Month )
					This.Value = GOMONTH( This.Value, 1 )

				Case lnKeyPress = 31	&& Ctrl+PageUp ( Previous Year )
					This.Value = GOMONTH( This.Value, -12 )

				Case lnKeyPress = 30	&& Ctrl+PageDn ( Next Year )
					This.Value = GOMONTH( This.Value, 12 )
			Endcase

		Endif
		

	Otherwise
		DoDefault( nButton, nShift, nXCoord, nYCoord )
		
Endcase

ENDPROCPROCEDURE Valid
IF EMPTY( THIS.VALUE )
	IF NOT THIS.lAllowBlankDate
		RETURN 0
	ENDIF
ELSE
	IF YEAR( THIS.VALUE ) = 0
		RETURN 0
	ENDIF
ENDIF

ENDPROC
PROCEDURE autofit
#INCLUDE "FW\Comunes\Include\Praxis.h"
Lparameters tnExtraLen As Integer,;
	tnExtraHeight As Integer

Local loTP As Object

Try
	tnExtraLen = IfEmpty( tnExtraLen, 2 )
	tnExtraHeight = IfEmpty( tnExtraHeight, 1.2 )
	
	loTP = Newobject("TextProperties","txtProperties.prg")

	If !loTP.Process( This, tnExtraLen, tnExtraHeight )
		Local loError As ErrorHandler Of "Tools\ErrorHandler\Prg\ErrorHandler.prg"
		loError = Newobject( "ErrorHandler", "Tools\ErrorHandler\Prg\ErrorHandler.prg" )
		loError.Process( loTP.oError )

	Endif


Catch To oErr


Finally
	loTP = .F.
	loError = .F.
	
Endtry

ENDPROC
PROCEDURE datepartinitfocus_assign
LPARAMETERS vNewVal
*To do: Modify this routine for the Assign method

IF INLIST( m.vNewVal, 1, 2, 3 )	&& Month, Day, Year
	THIS.DatePartInitFocus = m.vNewVal
ENDIF

ENDPROC
PROCEDURE lexitontabonly_assign
LPARAMETERS vNewVal
*To do: Modify this routine for the Assign method
IF VARTYPE( m.vNewVal ) = 'L'
	THIS.lExitOnTabOnly = m.vNewVal
ENDIF

ENDPROC
PROCEDURE lupdowndisabled_assign
LPARAMETERS vNewVal
*To do: Modify this routine for the Assign method
IF VARTYPE( vNewVal ) = 'L'
	THIS.lUpDownDisabled = m.vNewVal
ENDIF

ENDPROC
PROCEDURE setstartendpositions
*	We set the positions for testing when moving
*	between date parts, etc.
WITH THIS
	DO CASE
		CASE .DateType = 1			&& MDY 01/34/6789
			.DayStart = 3
			.DayEnd = 4
			.MonthStart = 0
			.MonthEnd = 1
			.YearStart = 6
			.YearEnd = 7			&& Century Off	(01/34/67)

			IF .YearSelLength = 4	&& Century On	(01/34/6789)
				.YearEnd = .YearEnd + 2
			ENDIF

		CASE .DateType = 2			&& DMY 01/34/6789
			.DayStart = 0
			.DayEnd = 1
			.MonthStart = 3
			.MonthEnd = 4
			.YearStart = 6
			.YearEnd = 7			&& Century Off	(01/34/67)

			IF .YearSelLength = 4	&& Century On	(01/34/6789)
				.YearEnd = .YearEnd + 2
			ENDIF

		OTHERWISE					&& YMD 0123/56/89
			.YearStart = 0

			.YearEnd = 1			&& Century Off	(01/34/67)
			.MonthStart = 3
			.MonthEnd = 4
			.DayStart = 6
			.DayEnd = 7

			IF .YearSelLength = 4	&& Century On	(0123/56/89)
				.YearEnd = .YearEnd + 2
				.MonthStart = .MonthStart + 2
				.MonthEnd = .MonthEnd + 2
				.DayStart = .DayStart + 2
				.DayEnd = .DayEnd + 2
			ENDIF
	ENDCASE
ENDWITH

ENDPROC
PROCEDURE setyearsellength
WITH THIS
	DO CASE
		CASE .Century = 0	&& Off
			.YearSelLength = 2

		CASE .Century = 1	&& On
			.YearSelLength = 4

		CASE .Century = 2	&& Default
			IF SET( "CENTURY" ) = "ON"
				.YearSelLength = 4
			ELSE
				.YearSelLength = 2
			ENDIF
	ENDCASE
ENDWITH

ENDPROC
PROCEDURE value_assign
LPARAMETERS vNewVal
*To do: Modify this routine for the Assign method
THIS.Value = m.vNewVal
If PemStatus( This.Parent, "Value", 5 )
	This.Parent.Value = m.vNewVal
EndIf
ENDPROC
[END METHODS]
[START RESERVED1]
Class[END RESERVED1]
[START RESERVED2]
1[END RESERVED2]
[START RESERVED3]
currentdate Date on field entry
lallowblankdate Allow blank dates
datetype Date Type 1-{mm/dd/yy},{mm-dd-yy},{mm.dd.yy}, 2-{dd/mm/yy},{dd-mm-yy},{dd.mm.yy}, 3-(yy/mm/dd),(yy-mm-dd),(yy.mm.dd)
datepartinitfocus Date part highlighted when control receives focus: 1- Month (default), 2-Day, 3-Year
yearsellength Selected length of the year ( 2 or 4 )
daystart Day Start position
dayend Day end position
monthstart Month start position
monthend Month end position
yearstart Year start position
yearend Year end position
lupdowndisabled Flag indicating whether the Up / Down arrow keys are disabled ( +/- functionality only )
lexitontabonly Exit the control only on (Shift+)Tab
fieldname
*datepartinitfocus_assign 
*setyearsellength Sets the year selected length
*setstartendpositions Sets the Day, Month and Year Start and End positions
*lupdowndisabled_assign 
*lexitontabonly_assign 
*value_assign 
*autofit Auto-resizes one or all visible grid columns.
[END RESERVED3]
[START RESERVED6]
Pixels[END RESERVED6]

[ RECORD]
[PLATFORM] COMMENT 
[UNIQUEID] RESERVED  
[OBJNAME] keydate
[START PROPERTIES]
Arial, 0, 9, 5, 15, 12, 32, 3, 0
[END PROPERTIES]
[START RESERVED1]
  ..\include\praxis.h¦•s~\V ..\include\foxpro.h®–ÊrûF ..\include\strings.h¶—ZŠOJ* ..\..\..\tools\namespaces\include\system.h¾˜±ÙF[END RESERVED1]
[EOF]
